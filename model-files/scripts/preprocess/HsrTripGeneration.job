;----------------------------------------------------------------------------------------------------------------
;
; HsrTripGeneration.job
;
; TP+ script to determine trips to and from Bay Area high speed rail stations based on output from the California
; High Speed Rail model.
;
; Starting with input trip tables for 2040 and 2050 (future modeled year), the script will assume zero
; trips before the opening year for the relevant station and interpolate the number of trips afterwards.
;
; Input:   Trip tables representing trips to/from the four Bay Area HSR stations in 2040 and 2050, with the
;          following four tables: (a) da. for drive alone, (b) sr2, for shared ride 2, (c) taxi, 
;          (d) transit, and (e) walk.
;
;          Additionally, the model year will be specified in the environment variable, MODEL_YEAR.
;
; Output:  Five, time-of-day-specific trip table matrices, each containing the following four tables: (a) da,
;          for drive alone, (b) sr2, for shared ride 2, (c) taxi, (d) transit, and (e) walk.
;
; See also: (1) IxTollChoice.job, which applies a toll/no toll logit model for the auto trips and
;               follows this script.
;           (2) HsrTransitSubmodeChoice.job, which chooses the transit submode for the transit trips and
;               also follows.
;
; version:  Travel Model One v1.6
; authors:  lmz (2023 11 07), lmz (2016 06 21)
;
; ----------------------------------------------------------------------------------------------------------------
model_year = %MODEL_YEAR%

; check if HSR_Interregional_Disable is configured
READ FILE="CTRAMP\scripts\block\trnParam.block"

loop tpnum=1,5

  if (tpnum=1)
    time_period = 'EA'
  elseif (tpnum=2)
    time_period = 'AM'
  elseif (tpnum=3)
    time_period = 'MD'
  elseif (tpnum=4)
    time_period = 'PM'
  elseif (tpnum=5)
    time_period = 'EV'
  endif

  DistributeMultistep processid = 'ctramp', processNum = tpnum, commpath = '%COMMPATH%'

  RUN PGM=MATRIX
    filei mati[1] = 'INPUT\nonres\tripsHsr@time_period@_2040.tpp'
    filei mati[2] = 'INPUT\nonres\tripsHsr@time_period@_2050.tpp'

    fileo mato[1] = 'nonres\tripsHsr@time_period@_intermediate.tpp', mo=1-10, 
       name=m_da, m_sr2, m_taxi, m_transit, m_walk, b_da, b_sr2, b_taxi, b_transit, b_walk

    ; if the model year is before HSR opening OR HSR interregional travel is disabled by configuration
    if ((@model_year@ < 2040) | (@HSR_Interregional_Disable@=1))
      ; nothing open
      MW[1] = 0
      MW[2] = 0
      MW[3] = 0
      MW[4] = 0
      MW[5] = 0

      MW[6] = 0
      MW[7] = 0
      MW[8] = 0
      MW[9] = 0
      MW[10]= 0
    else
      ; (times 100 is to keep more precision)
      ;        change in trips     change in years
      MW[1] = 100.0*(MI.2.da_veh   - MI.1.da_veh  )/(2050 - 2040)
      MW[2] = 100.0*(MI.2.sr2_veh  - MI.1.sr2_veh )/(2050 - 2040)
      MW[3] = 100.0*(MI.2.taxi_Veh - MI.1.taxi_Veh)/(2050 - 2040)
      MW[4] = 100.0*(MI.2.transit  - MI.1.transit )/(2050 - 2040)
      MW[5] = 100.0*(MI.2.walk     - MI.1.walk    )/(2050 - 2040)

      ;              trips         -  m x year
      MW[6] = 100.0*(MI.1.da_veh   - (MW[1]*0.01*2040))
      MW[7] = 100.0*(MI.1.sr2_veh  - (MW[2]*0.01*2040))
      MW[8] = 100.0*(MI.1.taxi_veh - (MW[3]*0.01*2040))
      MW[9] = 100.0*(MI.1.transit  - (MW[4]*0.01*2040))
      MW[10]= 100.0*(MI.1.walk     - (MW[5]*0.01*2040))
    endif
  ENDRUN

  RUN PGM=MATRIX
    filei mati[1] = 'nonres\tripsHsr@time_period@_intermediate.tpp'

    fileo mato[1] = 'nonres\tripsHsr@time_period@.tpp', mo=11-15, name=da_veh,sr2_veh,taxi_veh,transit,walk

    MW[11] = (0.01*MI.1.b_da     ) + (MI.1.m_da     *0.01*@model_year@)
    MW[12] = (0.01*MI.1.b_sr2    ) + (MI.1.m_sr2    *0.01*@model_year@)
    MW[13] = (0.01*MI.1.b_taxi   ) + (MI.1.m_taxi   *0.01*@model_year@)
    MW[14] = (0.01*MI.1.b_transit) + (MI.1.m_transit*0.01*@model_year@)
    MW[15] = (0.01*MI.1.b_walk   ) + (MI.1.m_walk   *0.01*@model_year@)

  ENDRUN

  EndDistributeMultistep

endloop

Wait4Files files=CTRAMP1.script.end, CTRAMP2.script.end, CTRAMP3.script.end, CTRAMP4.script.end, CTRAMP5.script.end,
           printfiles=merge, deldistribfiles=t, CheckReturnCode=t

; delete intermediate files
*del nonres\tripsHsrEA_intermediate.tpp
*del nonres\tripsHsrAM_intermediate.tpp
*del nonres\tripsHsrMD_intermediate.tpp
*del nonres\tripsHsrPM_intermediate.tpp
*del nonres\tripsHsrEV_intermediate.tpp
