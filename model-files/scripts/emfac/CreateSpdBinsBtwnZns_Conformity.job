;************************************************************************************
;*   1. Extract link level VMT and speeds;
;*   2. Extract total daily trips (for calculation of terminal distance VMT & speeds)
;*   (Subsequent SAS jobs will aggregate intrazonal and terminal distance VMT to
;*    the 13 CARB speed cohorts, for air quality analysis purposes.)
;*   Generic Forecast Year Run for all 5 timeperiods.
;*
;*   This Version accounts for the VMT double-counting/speed-bin overlap by using
;*   ">=" or "<" coding.
;*   This Version does NOT accounts for Futures Scenarios "AV" Vehicle Class VMT and
;*   is formatted appropriately for conformity analysis runs.
;*
;*
;*         -- March 9, 2020 ----        hmb.
;*
;* 
;*************************************************************************************


run pgm = hwynet

   ; loaded network assignment file
   neti[1] = hwy\iter3\AVGLOAD5PERIOD.net

   ; reset speeds to 25 mph for dummy links (these are dummy/centroid connector-access links)
   if (ft = 6)

      cspdEA = 25.0
      cspdAM = 25.0
      cspdMD = 25.0
      cspdPM = 25.0
      cspdEV = 25.0

   endif

   ; create an array that is indexed by time period x 10000 + county code x 100 + speed bin
   array _tmCtyBn = 51013
   array _tmCtyBn_NoTrk = 51013

   ; set the county code
   _countyCode = GL

   loop _time = 1,5

      if (_time = 1)

         _cspd = cspdEA
         _vmt           = (volEA_da + volEA_s2 + volEA_s3 + volEA_SM + volEA_HV + volEA_daT + volEA_s2T + volEA_s3T + volEA_SMT + volEA_HVT) * distance
         _vmt_NoTrk  = (volEA_da + volEA_s2 + volEA_s3 + volEA_daT + volEA_s2T + volEA_s3T) * distance

      elseif (_time = 2)

         _cspd = cspdAM
         _vmt           = (volAM_da + volAM_s2 + volAM_s3 + volAM_SM + volAM_HV + volAM_daT + volAM_s2T + volAM_s3T + volAM_SMT + volAM_HVT) * distance
         _vmt_NoTrk  = (volAM_da + volAM_s2 + volAM_s3 + volAM_daT + volAM_s2T + volAM_s3T) * distance

      elseif (_time = 3)

         _cspd = cspdMD
         _vmt           = (volMD_da + volMD_s2 + volMD_s3 + volMD_SM + volMD_HV + volMD_daT + volMD_s2T + volMD_s3T + volMD_SMT + volMD_HVT) * distance
         _vmt_NoTrk  = (volMD_da + volMD_s2 + volMD_s3 + volMD_daT + volMD_s2T + volMD_s3T) * distance

      elseif (_time = 4)

         _cspd = cspdPM
         _vmt           = (volPM_da + volPM_s2 + volPM_s3 + volPM_SM + volPM_HV + volPM_daT + volPM_s2T + volPM_s3T + volPM_SMT + volPM_HVT) * distance
         _vmt_NoTrk  = (volPM_da + volPM_s2 + volPM_s3 + volPM_daT + volPM_s2T + volPM_s3T) * distance

      elseif (_time = 5)

         _cspd = cspdEV
         _vmt           = (volEV_da + volEV_s2 + volEV_s3 + volEV_SM + volEV_HV + volEV_daT + volEV_s2T + volEV_s3T  + volEV_SM + volEV_HVT) * distance
         _vmt_NoTrk  = (volEV_da + volEV_s2 + volEV_s3 + volEV_daT + volEV_s2T + volEV_s3T) * distance
      endif

      loop _speed = 1, 13

         if (_speed = 1)

            _speedBinLower = 0.0
            _speedBinUpper = 4.999

         elseif (_speed = 2)

            _speedBinLower = 5.000
            _speedBinUpper = 9.999

         elseif (_speed = 3)

            _speedBinLower = 10.000
            _speedBinUpper = 14.999

         elseif (_speed = 4)

            _speedBinLower = 15.000
            _speedBinUpper = 19.999

         elseif (_speed = 5)

            _speedBinLower = 20.000
            _speedBinUpper = 24.999

         elseif (_speed = 6)

            _speedBinLower = 25.000
            _speedBinUpper = 29.999

         elseif (_speed = 7)

            _speedBinLower = 30.000
            _speedBinUpper = 34.999

         elseif (_speed = 8)

            _speedBinLower = 35.000
            _speedBinUpper = 39.999

         elseif (_speed = 9)

            _speedBinLower = 40.000
            _speedBinUpper = 44.999

         elseif (_speed = 10)

            _speedBinLower = 45.000
            _speedBinUpper = 49.999

         elseif (_speed = 11)

            _speedBinLower = 50.000
            _speedBinUpper = 54.999

         elseif (_speed = 12)

            _speedBinLower = 55.000
            _speedBinUpper = 59.999

         elseif (_speed = 13)

            _speedBinLower = 60.000
            _speedBinUpper = 75.001
            ; max free flow speed in the travel model network is 75 mph


         endif

         if (_cspd >= _speedBinLower && _cspd < _speedBinUpper)

            _arrayIndex = _time * 10000 + _countyCode * 100 + _speed
            _tmCtyBn[_arrayIndex]          = _tmCtyBn[_arrayIndex] + _vmt
            _tmCtyBn_NoTrk[_arrayIndex]   = _tmCtyBn_NoTrk[_arrayIndex] + _vmt_NoTrk

         endif

      endloop ; _speed

   endloop ; _time


   ; dump to a csv

   phase = summary

      ; set arrays for sum vmt and share of vmt in each hour
      array _vmtArray = 5000
      array _vmtArray_NoTrk = 5000
      array _shareVmt = 5000
      array _shareVmt_NoTrk = 5000

      list = "countyName, arbCounty, speedBin, hour01, hour02, hour03, hour04, hour05, hour06,",
             "hour07, hour08, hour09, hour10, hour11, hour12, hour13, hour14, hour15, hour16,",
             "hour17, hour18, hour19, hour20, hour21, hour22, hour23, hour24",
             file = emfac3\Create_AllVehs_SpeedBinsBetweenZones_sums.csv

      list = "countyName, arbCounty, speedBin, hour01, hour02, hour03, hour04, hour05, hour06,",
             "hour07, hour08, hour09, hour10, hour11, hour12, hour13, hour14, hour15, hour16,",
             "hour17, hour18, hour19, hour20, hour21, hour22, hour23, hour24",
             file = emfac3\CreateSpeedBinsBetweenZones_sums_NoTrk.csv


      ; build an array to let us write out the results in alphabetical order
      array _alphaCounty = 10
      _alphaCounty[1]  = 4
      _alphaCounty[2]  = 5
      _alphaCounty[3]  = 9
      _alphaCounty[4]  = 7
      _alphaCounty[5]  = 1
      _alphaCounty[6]  = 2
      _alphaCounty[7]  = 3
      _alphaCounty[8]  = 6
      _alphaCounty[9]  = 8
      _alphaCounty[10] = 10

      ; give each county a name and an ARB code
      loop _alphaCounter = 1,10

         ; set the county variable which is used in all the storage arrays
         _county = _alphaCounty[_alphaCounter]

         if (_county = 1)
            _arbCounty   = 38
            _countyName  = 'San Francisco'

         elseif (_county = 2)
            _arbCounty = 41
            _countyName = 'San Mateo'

         elseif (_county = 3)
            _arbCounty = 43
            _countyName = 'Santa Clara'

         elseif (_county = 4)
            _arbCounty = 01
            _countyName = 'Alameda'

         elseif (_county = 5)
            _arbCounty = 07
            _countyName = 'Contra Costa'

         elseif (_county = 6)
            _arbCounty = 48
            _countyName = 'Solano'

         elseif (_county = 7)
            _arbCounty = 28
            _countyName = 'Napa'

         elseif (_county = 8)
            _arbCounty = 49
            _countyName = 'Sonoma'

         elseif (_county = 9)
            _arbCounty = 21
            _countyName = 'Marin'

         elseif (_county = 10)
            _arbCounty = 9999
            _countyName = 'External Zones'

         endif


         ; first loop to do calculations
         loop _time24 = 1,24

            if (_time24>3  && _time24<=6)

               _timeIndex = 1

               ; set within time period diurnal factor consistent with EMFAC
               if (_time24 = 4) _diurnalFactor = 0.157
               if (_time24 = 5) _diurnalFactor = 0.298
               if (_time24 = 6) _diurnalFactor = 0.545

            elseif (_time24>6  && _time24<=10)

               _timeIndex = 2

               if (_time24 = 7)  _diurnalFactor = 0.164
	       if (_time24 = 8)  _diurnalFactor = 0.336
               if (_time24 = 9)  _diurnalFactor = 0.309
               if (_time24 = 10) _diurnalFactor = 0.191


             elseif (_time24>10  && _time24<=15)

               _timeIndex = 3

               if (_time24 = 11) _diurnalFactor = 0.157
	       if (_time24 = 12) _diurnalFactor = 0.198
	       if (_time24 = 13) _diurnalFactor = 0.207
               if (_time24 = 14) _diurnalFactor = 0.203
               if (_time24 = 15) _diurnalFactor = 0.235


            elseif (_time24>15 && _time24<=19)

               _timeIndex = 4

               if (_time24 = 16) _diurnalFactor = 0.251
	       if (_time24 = 17) _diurnalFactor = 0.261
	       if (_time24 = 18) _diurnalFactor = 0.288
               if (_time24 = 19) _diurnalFactor = 0.200

            elseif (_time24>19 || _time24<=3)

               _timeIndex     = 5

               if (_time24 = 20) _diurnalFactor = 0.248
	       if (_time24 = 21) _diurnalFactor = 0.190
	       if (_time24 = 22) _diurnalFactor = 0.192
               if (_time24 = 23) _diurnalFactor = 0.144
               if (_time24 = 24) _diurnalFactor = 0.109

	       if (_time24 = 1)  _diurnalFactor = 0.067
	       if (_time24 = 2)  _diurnalFactor = 0.025
               if (_time24 = 3)  _diurnalFactor = 0.025

            endif


            _sumVmt = 0.0

            loop _speedBin = 1,13

              _index   = _timeIndex * 10000 + _county * 100 + _speedBin
              _tempVmt          = _tmCtyBn[_index] * _diurnalFactor
              _tempVmt_NoTrk = _tmCtyBn_NoTrk[_index] * _diurnalFactor

              _sumIndex = _speedBin * 100 + _time24
              _sumVmt          = _sumVmt + _tempVmt
              _sumVmt_NoTrk = _sumVmt_NoTrk + _tempVmt_NoTrk

            endloop

            loop _speedBin = 1,13

              _index          = _timeIndex * 10000 + _county * 100 + _speedBin
              _tempVmt        = _tmCtyBn[_index] * _diurnalFactor
              _tempVmt_NoTrk  = _tmCtyBn_NoTrk[_index] * _diurnalFactor

              _sumIndex = _speedBin * 100 + _time24
              _shareVmt[_sumIndex] = 0.0

              if (_sumVmt > 0.0)

                 _shareVmt[_sumIndex]          = _tempVmt/_sumVmt
                 _shareVmt_NoTrk[_sumIndex]    = _tempVmt_NoTrk/_sumVmt_NoTrk
                 _vmtArray[_sumIndex]          = _tempVmt
                 _vmtArray_NoTrk[_sumIndex]    = _tempVmt_NoTrk

              endif

            endloop ; _speedBin

         endloop ; _time24

         loop _speedBin = 1,13

            list = _countyName,",",_arbCounty,",",_speedBin,",",
                   _vmtArray[_speedBin * 100 + 1](12.2), ",", _vmtArray[_speedBin * 100 + 2](12.2), ",", _vmtArray[_speedBin * 100 + 3](12.2), ",",
                   _vmtArray[_speedBin * 100 + 4](12.2), ",", _vmtArray[_speedBin * 100 + 5](12.2), ",", _vmtArray[_speedBin * 100 + 6](12.2), ",",
                   _vmtArray[_speedBin * 100 + 7](12.2), ",", _vmtArray[_speedBin * 100 + 8](12.2), ",", _vmtArray[_speedBin * 100 + 9](12.2), ",",
                   _vmtArray[_speedBin * 100 + 10](12.2),",", _vmtArray[_speedBin * 100 + 11](12.2),",", _vmtArray[_speedBin * 100 + 12](12.2),",",
                   _vmtArray[_speedBin * 100 + 13](12.2),",", _vmtArray[_speedBin * 100 + 14](12.2),",", _vmtArray[_speedBin * 100 + 15](12.2),",",
                   _vmtArray[_speedBin * 100 + 16](12.2),",", _vmtArray[_speedBin * 100 + 17](12.2),",", _vmtArray[_speedBin * 100 + 18](12.2),",",
                   _vmtArray[_speedBin * 100 + 19](12.2),",", _vmtArray[_speedBin * 100 + 20](12.2),",", _vmtArray[_speedBin * 100 + 21](12.2),",",
                   _vmtArray[_speedBin * 100 + 22](12.2),",", _vmtArray[_speedBin * 100 + 23](12.2),",", _vmtArray[_speedBin * 100 + 24](12.2),
                   file = emfac3\Create_AllVehs_SpeedBinsBetweenZones_sums.csv

            list = _countyName,",",_arbCounty,",",_speedBin,",",
                   _vmtArray_NoTrk[_speedBin * 100 + 1](12.2), ",", _vmtArray_NoTrk[_speedBin * 100 + 2](12.2), ",", _vmtArray_NoTrk[_speedBin * 100 + 3](12.2), ",",
                   _vmtArray_NoTrk[_speedBin * 100 + 4](12.2), ",", _vmtArray_NoTrk[_speedBin * 100 + 5](12.2), ",", _vmtArray_NoTrk[_speedBin * 100 + 6](12.2), ",",
                   _vmtArray_NoTrk[_speedBin * 100 + 7](12.2), ",", _vmtArray_NoTrk[_speedBin * 100 + 8](12.2), ",", _vmtArray_NoTrk[_speedBin * 100 + 9](12.2), ",",
                   _vmtArray_NoTrk[_speedBin * 100 + 10](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 11](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 12](12.2),",",
                   _vmtArray_NoTrk[_speedBin * 100 + 13](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 14](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 15](12.2),",",
                   _vmtArray_NoTrk[_speedBin * 100 + 16](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 17](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 18](12.2),",",
                   _vmtArray_NoTrk[_speedBin * 100 + 19](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 20](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 21](12.2),",",
                   _vmtArray_NoTrk[_speedBin * 100 + 22](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 23](12.2),",", _vmtArray_NoTrk[_speedBin * 100 + 24](12.2),
                   file = emfac3\CreateSpeedBinsBetweenZones_sums_NoTrk.csv

          endloop

      endloop ; _county

   endphase

endrun
;*********************************************************************************************
