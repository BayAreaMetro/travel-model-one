; ----------------------------------------------------------------------------------------------------------------
;
; TruckTimeOfDay.job
;
; TP+ script to segment daily estimates of truck flows into time-period-specific flows.  The time periods are: 
; early AM, 3 am to 6 am; AM peak, 6 am to 10 am; midday, 10 am to 3 pm; PM peak, 3 pm to 7 pm; and evening, 
; 7 pm to 3 am the next day. The four truck types are: very small trucks (two-axle, four-tire), small trucks 
; (two-axle, six-tire), medium trucks (three-axle), and large or combination (four or more axle) trucks.
;
; The diurnal factors are taken from the BAYCAST-90 model with adjustments made during calibration to the very
; small truck values to better match counts. 
;
; Input:   A four-table production/attraction trip table matrix of daily class-specific truck trips (in units 
;          of trips x 100, to be consistent with the previous application)with a table for (a) very small trucks,
;          (b) small trucks, (c) medium trucks, and (d) large trucks.
;
; Output: Five, time-of-day-specific trip table matrices, each containing the following four tables: (a) vstruck,
;         for very small trucks, (b) struck, for small trucks, (c) mtruck, for medium trucks, and (d) ctruck,
;         for combination truck. 
;
; Notes:  (1) These scripts do not update the BAYCAST truck model; rather, the model is simply implemented in a
;             manner consistent with the Travel Model One implementation
;
; See also: (1) TruckTripDistribution.job, which applies a gravity distribution model and precedes this script.
;           (2) TruckTollChoice.job, which applies a toll/no toll logit model and follows this script.
;
; version:  Travel Model One
; authors:  dto (2011 06 24); jh (2010)
;
;
; ----------------------------------------------------------------------------------------------------------------

run pgm = matrix

   ; Daily trips from the TruckTripDistribution model
   filei mati[1] = nonres\DailyTruckTrips.tpp        

   ; Added Nov 2022 for NextGen Freeways Study: 
   ; Daily Ix truck trips from IxTimeOfDay.job
   filei mati[2] = nonres\ixDailyx4_truck.tpp

   ; Time-of-day specific truck tables
   fileo mato[1] = nonres\TripsTrkEAx.tpp, mo=11-14, name = vstruck, struck, mtruck, ctruck
   fileo mato[2] = nonres\TripsTrkAMx.tpp, mo=15-18, name = vstruck, struck, mtruck, ctruck
   fileo mato[3] = nonres\TripsTrkMDx.tpp, mo=19-22, name = vstruck, struck, mtruck, ctruck
   fileo mato[4] = nonres\TripsTrkPMx.tpp, mo=23-26, name = vstruck, struck, mtruck, ctruck
   fileo mato[5] = nonres\TripsTrkEVx.tpp, mo=27-30, name = vstruck, struck, mtruck, ctruck
   
   ; convert the P/A matrices to O/D (and scale DailyTruckTrips by 100, as distribution outputs trips x 100)
   mw[1] = (0.5 * mi.1.1 + 0.5 * mi.1.1.T)/100.0 + (0.5 * mi.2.vsm_daily + 0.5*mi.2.vsm_daily.T)
   mw[2] = (0.5 * mi.1.2 + 0.5 * mi.1.2.T)/100.0 + (0.5 * mi.2.sml_daily + 0.5*mi.2.sml_daily.T)
   mw[3] = (0.5 * mi.1.3 + 0.5 * mi.1.3.T)/100.0 + (0.5 * mi.2.med_daily + 0.5*mi.2.med_daily.T)
   mw[4] = (0.5 * mi.1.4 + 0.5 * mi.1.4.T)/100.0 + (0.5 * mi.2.lrg_daily + 0.5*mi.2.lrg_daily.T)
   
   ; apply class-specific diurnal factors - very small truck factors adjusted during calibration
   
   ;     - early AM
   ;          - very small trucks
   mw[11] = 0.0235 * mw[1]     
   
   ;          - small trucks
   mw[12] = 0.0765 * mw[2]      
   
   ;          - medium trucks
   mw[13] = 0.0665 * mw[3]     
   
   ;          - combination trucks
   mw[14] = 0.1430 * mw[4]      
   
   ;     - AM peak
   mw[15] = 0.0000 * mw[1]      
   mw[16] = 0.2440 * mw[2]      
   mw[17] = 0.2930 * mw[3]      
   mw[18] = 0.2320 * mw[4] 
   
   ;     - midday
   mw[19] = 0.6080 * mw[1]     
   mw[20] = 0.3710 * mw[2]      
   mw[21] = 0.3935 * mw[3]  
   mw[22] = 0.3315 * mw[4]      
   
   ;     - PM peak
   mw[23] = 0.1980 * mw[1]      
   mw[24] = 0.2180 * mw[2]     
   mw[25] = 0.1730 * mw[3]      
   mw[26] = 0.1750 * mw[4]     
   
   ;     - evening
   mw[27] = 0.1705 * mw[1]      
   mw[28] = 0.0905 * mw[2]      
   mw[29] = 0.0740 * mw[3]     
   mw[30] = 0.1185 * mw[4]      
  
endrun