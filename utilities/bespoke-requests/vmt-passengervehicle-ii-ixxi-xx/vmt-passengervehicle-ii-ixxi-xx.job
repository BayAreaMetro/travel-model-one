; As requested by CARB, this script summarizes:
;   - Total VMT per weekday for passenger vehicles (ARB vehicle classes LDA, LDT1, LDT2, and MDV)
;     (Per https://www.arb.ca.gov/msei/vehicle-categories.xlsx these are:
;       LDA = Passenger Cars,
;       LDT1 & LDT2 = Light Duty Trucks (not Light-Heavy Duty Trucks)
;       MDF = Medium Duty Trucks
;   - Total II VMT per weekday for passenger vehicles (miles)
;   - Total IX/XI VMT per weekday for passenger vehicles (miles)
;   - Total XX VMT per weekday for passenger vehicles (miles)
;
; lmz 2017.10.04

RUN PGM = MATRIX
  parameters zones = 1475
  FILEO MATO[1]="metrics\II_IXXI_XX.mat",  MO=1-3, NAME=II,IXXI,XX

  MW[1] = 0
  MW[2] = 0
  MW[3] = 0

  JLOOP
    IF ((I=1-1454) && (J=1-1454))              ; II
      MW[1] = 1
    ELSEIF ((I=1-1454) && (J=1455-1475))       ; IX
      MW[2] = 1
    ELSEIF ((I=1455-1475) && (J=1-1454))       ; XI
      MW[2] = 1
    ELSEIF ((I=1455-1475) && (J=1455-1475))    ; XX
      MW[3] = 1
    ENDIF
  ENDJLOOP
ENDRUN

RUN PGM = MATRIX

  ; Read Highway Skims
  FILEI MATI[1] = "skims\HWYSKMEA.tpp"
  FILEI MATI[2] = "skims\HWYSKMAM.tpp"
  FILEI MATI[3] = "skims\HWYSKMMD.tpp"
  FILEI MATI[4] = "skims\HWYSKMPM.tpp"
  FILEI MATI[5] = "skims\HWYSKMEV.tpp"

  ; Read person trips
  FILEI MATI[6 ] = "main\tripsEA.tpp"
  FILEI MATI[7 ] = "main\tripsAM.tpp"
  FILEI MATI[8 ] = "main\tripsMD.tpp"
  FILEI MATI[9 ] = "main\tripsPM.tpp"
  FILEI MATI[10] = "main\tripsEV.tpp"

  FILEI MATI[20]  = "metrics\II_IXXI_XX.mat"
  FILEO PRINTO[1] = "metrics\II_IXXI_XX.csv",APPEND=F

  ; Person Trip Vehicle Miles
  MW[1] = ((mi.1.DISTDA    *mi.6.da     ) + (mi.2.DISTDA    *mi.7.da     ) + (mi.3.DISTDA    *mi.8.da     ) + (mi.4.DISTDA    *mi.9.da     ) + (mi.5.DISTDA    *mi.10.da     ))/1.0 +
          ((mi.1.TOLLDISTDA*mi.6.datoll ) + (mi.2.TOLLDISTDA*mi.7.datoll ) + (mi.3.TOLLDISTDA*mi.8.datoll ) + (mi.4.TOLLDISTDA*mi.9.datoll ) + (mi.5.TOLLDISTDA*mi.10.datoll ))/1.0 +
          ((mi.1.DISTS2    *mi.6.sr2    ) + (mi.2.DISTS2    *mi.7.sr2    ) + (mi.3.DISTS2    *mi.8.sr2    ) + (mi.4.DISTS2    *mi.9.sr2    ) + (mi.5.DISTS2    *mi.10.sr2    ))/2.0 +
          ((mi.1.TOLLDISTDA*mi.6.sr2toll) + (mi.2.TOLLDISTDA*mi.7.sr2toll) + (mi.3.TOLLDISTDA*mi.8.sr2toll) + (mi.4.TOLLDISTDA*mi.9.sr2toll) + (mi.5.TOLLDISTDA*mi.10.sr2toll))/2.0 +
          ((mi.1.DISTS3    *mi.6.sr3    ) + (mi.2.DISTS3    *mi.7.sr3    ) + (mi.3.DISTS3    *mi.8.sr3    ) + (mi.4.DISTS3    *mi.9.sr3    ) + (mi.5.DISTS3    *mi.10.sr3    ))/3.5 +
          ((mi.1.TOLLDISTS3*mi.6.sr3toll) + (mi.2.TOLLDISTS3*mi.7.sr3toll) + (mi.3.TOLLDISTS3*mi.8.sr3toll) + (mi.4.TOLLDISTS3*mi.9.sr3toll) + (mi.5.TOLLDISTS3*mi.10.sr3toll))/3.5

  ; create II, IXXI, XX
  MW[4] = (MW[1])*mi.20.II
  MW[5] = (MW[1])*mi.20.IXXI
  MW[6] = (MW[1])*mi.20.XX

  VMT_II   = VMT_II   + ROWSUM(4)
  VMT_IXXI = VMT_IXXI + ROWSUM(5)
  VMT_XX   = VMT_XX   + ROWSUM(6)
  IF (I=1475)
    PRINT PRINTO=1 CSV=T, LIST="Category, VMT II, VMT IXXI, VMT XX"
    PRINT PRINTO=1 CSV=T, LIST="Residents",VMT_II(10.2L), VMT_IXXI(10.2L), VMT_XX(10.2L)
  ENDIF

ENDRUN

RUN PGM = MATRIX

  ; Read Highway Skims
  FILEI MATI[1] = "skims\HWYSKMEA.tpp"
  FILEI MATI[2] = "skims\HWYSKMAM.tpp"
  FILEI MATI[3] = "skims\HWYSKMMD.tpp"
  FILEI MATI[4] = "skims\HWYSKMPM.tpp"
  FILEI MATI[5] = "skims\HWYSKMEV.tpp"

  ; Read air passenger travel demand - vehicle trips
  FILEI MATI[6]  = "nonres\tripsAirPaxEA.tpp"
  FILEI MATI[7]  = "nonres\tripsAirPaxAM.tpp"
  FILEI MATI[8]  = "nonres\tripsAirPaxMD.tpp"
  FILEI MATI[9]  = "nonres\tripsAirPaxPM.tpp"
  FILEI MATI[10] = "nonres\tripsAirPaxEV.tpp"

  ; Read internal/external travel demand - vehicle trips
  FILEI MATI[11] = "nonres\tripsIxEA.tpp"
  FILEI MATI[12] = "nonres\tripsIxAM.tpp"
  FILEI MATI[13] = "nonres\tripsIxMD.tpp"
  FILEI MATI[14] = "nonres\tripsIxPM.tpp"
  FILEI MATI[15] = "nonres\tripsIxEV.tpp"


  FILEI MATI[20]  = "metrics\II_IXXI_XX.mat"
  FILEO PRINTO[1] = "metrics\II_IXXI_XX.csv",APPEND=T

  ; Air passenger Vehicle Miles
  MW[2] = ((mi.1.DISTDA    *mi.6.da     ) + (mi.2.DISTDA    *mi.7.da     ) + (mi.3.DISTDA    *mi.8.da     ) + (mi.4.DISTDA    *mi.9.da     ) + (mi.5.DISTDA    *mi.10.da     )) +
          ((mi.1.TOLLDISTDA*mi.6.datoll ) + (mi.2.TOLLDISTDA*mi.7.datoll ) + (mi.3.TOLLDISTDA*mi.8.datoll ) + (mi.4.TOLLDISTDA*mi.9.datoll ) + (mi.5.TOLLDISTDA*mi.10.datoll )) +
          ((mi.1.DISTS2    *mi.6.sr2    ) + (mi.2.DISTS2    *mi.7.sr2    ) + (mi.3.DISTS2    *mi.8.sr2    ) + (mi.4.DISTS2    *mi.9.sr2    ) + (mi.5.DISTS2    *mi.10.sr2    )) +
          ((mi.1.TOLLDISTDA*mi.6.sr2toll) + (mi.2.TOLLDISTDA*mi.7.sr2toll) + (mi.3.TOLLDISTDA*mi.8.sr2toll) + (mi.4.TOLLDISTDA*mi.9.sr2toll) + (mi.5.TOLLDISTDA*mi.10.sr2toll)) +
          ((mi.1.DISTS3    *mi.6.sr3    ) + (mi.2.DISTS3    *mi.7.sr3    ) + (mi.3.DISTS3    *mi.8.sr3    ) + (mi.4.DISTS3    *mi.9.sr3    ) + (mi.5.DISTS3    *mi.10.sr3    )) +
          ((mi.1.TOLLDISTS3*mi.6.sr3toll) + (mi.2.TOLLDISTS3*mi.7.sr3toll) + (mi.3.TOLLDISTS3*mi.8.sr3toll) + (mi.4.TOLLDISTS3*mi.9.sr3toll) + (mi.5.TOLLDISTS3*mi.10.sr3toll))

  ; IX/XI Person Trip Vehicle Miles
  MW[3] = ((mi.1.DISTDA    *mi.11.da     ) + (mi.2.DISTDA    *mi.12.da     ) + (mi.3.DISTDA    *mi.13.da     ) + (mi.4.DISTDA    *mi.14.da     ) + (mi.5.DISTDA    *mi.15.da     )) +
          ((mi.1.TOLLDISTDA*mi.11.datoll ) + (mi.2.TOLLDISTDA*mi.12.datoll ) + (mi.3.TOLLDISTDA*mi.13.datoll ) + (mi.4.TOLLDISTDA*mi.14.datoll ) + (mi.5.TOLLDISTDA*mi.15.datoll )) +
          ((mi.1.DISTS2    *mi.11.sr2    ) + (mi.2.DISTS2    *mi.12.sr2    ) + (mi.3.DISTS2    *mi.13.sr2    ) + (mi.4.DISTS2    *mi.14.sr2    ) + (mi.5.DISTS2    *mi.15.sr2    )) +
          ((mi.1.TOLLDISTDA*mi.11.sr2toll) + (mi.2.TOLLDISTDA*mi.12.sr2toll) + (mi.3.TOLLDISTDA*mi.13.sr2toll) + (mi.4.TOLLDISTDA*mi.14.sr2toll) + (mi.5.TOLLDISTDA*mi.15.sr2toll)) +
          ((mi.1.DISTS3    *mi.11.sr3    ) + (mi.2.DISTS3    *mi.12.sr3    ) + (mi.3.DISTS3    *mi.13.sr3    ) + (mi.4.DISTS3    *mi.14.sr3    ) + (mi.5.DISTS3    *mi.15.sr3    )) +
          ((mi.1.TOLLDISTS3*mi.11.sr3toll) + (mi.2.TOLLDISTS3*mi.12.sr3toll) + (mi.3.TOLLDISTS3*mi.13.sr3toll) + (mi.4.TOLLDISTS3*mi.14.sr3toll) + (mi.5.TOLLDISTS3*mi.15.sr3toll))

  ; create II, IXXI, XX
  MW[4] = (MW[2] + MW[3])*mi.20.II
  MW[5] = (MW[2] + MW[3])*mi.20.IXXI
  MW[6] = (MW[2] + MW[3])*mi.20.XX

  VMT_II   = VMT_II   + ROWSUM(4)
  VMT_IXXI = VMT_IXXI + ROWSUM(5)
  VMT_XX   = VMT_XX   + ROWSUM(6)
  IF (I=1475)
    PRINT PRINTO=1 CSV=T, LIST="NonRes",VMT_II(10.2L), VMT_IXXI(10.2L), VMT_XX(10.2L)
  ENDIF

ENDRUN

RUN PGM = MATRIX
  ; Read truck trips for all time periods
  FILEI MATI[1] = "nonres\tripstrkEA.tpp"
  FILEI MATI[2] = "nonres\tripstrkAM.tpp"
  FILEI MATI[3] = "nonres\tripstrkMD.tpp"
  FILEI MATI[4] = "nonres\tripstrkPM.tpp"
  FILEI MATI[5] = "nonres\tripstrkEV.tpp"

  ; Read Truck Highway Skims
  FILEI MATI[6] = "skims\COM_HWYSKIMEA.tpp"
  FILEI MATI[7] = "skims\COM_HWYSKIMAM.tpp"
  FILEI MATI[8] = "skims\COM_HWYSKIMMD.tpp"
  FILEI MATI[9] = "skims\COM_HWYSKIMPM.tpp"
  FILEI MATI[10]= "skims\COM_HWYSKIMEV.tpp"

  FILEI MATI[20]  = "metrics\II_IXXI_XX.mat"
  FILEO PRINTO[1] = "metrics\II_IXXI_XX.csv",APPEND=T

  MW[1]   = (mi.1.vstruck * mi.6.distvsm ) + (mi.2.vstruck * mi.7.distvsm ) + (mi.3.vstruck * mi.8.distvsm ) + (mi.4.vstruck * mi.9.distvsm ) + (mi.5.vstruck * mi.10.distvsm ) +
            (mi.1.struck  * mi.6.distsml ) + (mi.2.struck  * mi.7.distsml ) + (mi.3.struck  * mi.8.distsml ) + (mi.4.struck  * mi.9.distsml ) + (mi.5.struck  * mi.10.distsml ) +
            (mi.1.mtruck  * mi.6.distmed ) + (mi.2.mtruck  * mi.7.distmed ) + (mi.3.mtruck  * mi.8.distmed ) + (mi.4.mtruck  * mi.9.distmed ) + (mi.5.mtruck  * mi.10.distmed )

  ; create II, IXXI, XX
  MW[4] = (MW[1] + MW[2] + MW[3])*mi.20.II
  MW[5] = (MW[1] + MW[2] + MW[3])*mi.20.IXXI
  MW[6] = (MW[1] + MW[2] + MW[3])*mi.20.XX

  VMT_II   = VMT_II   + ROWSUM(4)
  VMT_IXXI = VMT_IXXI + ROWSUM(5)
  VMT_XX   = VMT_XX   + ROWSUM(6)
  IF (I=1475)
    PRINT PRINTO=1 CSV=T, LIST="SmMedTrucks",VMT_II(10.2L), VMT_IXXI(10.2L), VMT_XX(10.2L)
  ENDIF

ENDRUN