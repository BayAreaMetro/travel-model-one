
; HwyAssign_selectLink_findODs.job - compare to HwyAssign.job

read file = ctramp\scripts\block\HwyIntraStep_AM.block
token_link = '%SELECT_LINK%'

; LETS JUST DO AM
loop period = 2,2

      token_period = 'AM'
      token_capfac = 'parameters capfac = 4'
      token_intrastep_processlist = token_intrastep_processlist_AM
      token_hov_xfer_pen = 'li.HOVXPEN'
      token_relativegap = 'parameters relativegap = 0.0005'
      token_maxiters = 'parameters maxiters = 1000'

   ; distribute via cluster
   DistributeMultistep processid = 'CTRAMP', processNum = @period@, commpath = 'm:\commpath'
  
   run pgm = highway
  
      ; distribute via cluster
      DistributeIntrastep processid = 'CTRAMP', processlist = @token_intrastep_processlist@, commpath = 'm:\commpath', saveprn = f
    
      ; time-specific input highway network
      neti    = hwy\avgload@token_period@.net
      
      ; household travel demand
      mati[1] = main\trips@token_period@.tpp
      
      ; internal/external travel demand
      mati[2] = nonres\tripsIx@token_period@.tpp 
      
      ; commercial travel demand
      mati[3] = nonres\tripsTrk@token_period@.tpp
      
      ; air passenger travel demand
      mati[4] = nonres\tripsAirPax@token_period@.tpp
    
      ; loaded time-specific highway network
      ; neto    = hwy\load@token_period@.net
      neto    = "hwy\selectlink\load@token_period@_selectlink_@token_link@_ODs_v2.net"
      mato    = "hwy\selectlink\load@token_period@_selectlink_@token_link@_ODs_v2.mat", MO=1-10, NAME=vol_da,vol_s2,vol_s3,vol_sm,vol_hv,vol_daT,vol_s2T,vol_s3T,vol_vmT,vol_hvT

      ; set the assignment paramters -- equilibrium assignment, frank-wolfe
      parameters combine = equi; , enhance = 2
      
      ; set the assignment parameters -- 1454 internal zones + 21 external zones
      parameters zones = 1475
      
      ; set the assignment parameters -- maximum number of iterations to 300
      @token_maxiters@ 
      
      ; set the assignment parameters -- closure criteria (tighten up as runtime allows)
      @token_relativegap@ 
      
      ; set the assignment parameters -- other closure criteria (do not use)
      parameters gap = 0, aad = 0, raad = 0, rmse = 0
    
      ; set the number of hours in the assignment (see the loop tokens)
      @token_capfac@ 
    
      ; hwyParam.bock contains variables used in the remainder of the script, particularly in the generalized cost expressions
      read file = ctramp\scripts\block\hwyparam.block
      
      ; read in the speed/capacity table
      read file = ctramp\scripts\block\SpeedCapacity_1hour.block 
      
      ; set the working variables
      phase = linkread
      
         ; distance is used in the volume/delay functions and the generalized cost function
         distance = li.DISTANCE
         
         ; facility type is used in the compuation of the volume/delay function parameter
         linkclass = li.FT
         
         ; tollclass determines whether a link is only available to value toll payers
         lw.tollclass = li.TOLLCLASS
         
         ; fixed time links (where TSIN = 1) are indifferent to congestion
         lw.ot = li.OT
         
         ; free-flow speed is used in the volume/delay functions
         lw.ffs = li.FFS
         
         ; free-flow time is a function of free-flow speed and distance and is used in the generalized cost function
         lw.fft = (distance/lw.ffs) * 60 
         
         ; TSIN is a code, where 1 indicates the link has a fixed time and is immune to demand
         lw.tsin = li.TSIN
         
         ; link capacity is a function of the number of lanes
         lw.lanes = li.LANES
         
         ; the speed/capacity table and the computation of the volume/delay function parameter (see FreeFlowSpeed.block) uses the capacity class code
         ; which is area type * 10 + facility type
         lw.capclass = li.CAPCLASS
         
         ; set the input time to OT for fixed time links
         if (lw.tsin = 1)
            t0 = lw.ot
         endif
      
        ; the setting of critical speeds and computation of the volume/delay function parameter is in the FreeFlowSpeed.block file
        read file = ctramp\scripts\block\FreeFlowSpeed.block
    
        ; compute the generalized cost for passenger vehicles
         lw.gencost_da = lw.fft + (0.6/vot) * (lw.dist*li.autoopc + li.TOLL@token_period@_DA)
         lw.gencost_s2 = lw.fft + (0.6/vot) * (lw.dist*li.autoopc + li.TOLL@token_period@_S2 / sr2costshare)  + @token_hov_xfer_pen@
         lw.gencost_s3 = lw.fft + (0.6/vot) * (lw.dist*li.autoopc + li.TOLL@token_period@_S3 / sr3costshare)  + @token_hov_xfer_pen@
         
         ; compute the generalized cost for very small, small, and medium trucks (use small truck costs) and large trucks
         lw.gencost_sml = lw.fft + (0.6/truckvot) * (lw.dist*li.smtropc + li.TOLL@token_period@_SML)
         lw.gencost_lrg = lw.fft + (0.6/truckvot) * (lw.dist*li.lrtropc + li.TOLL@token_period@_LRG)
         
         ; create the link exclusion classes -- HOV only (23), HOV 3+ only (3), no large trucks (4)                
         if (li.USE = 2,3)   addtogroup = 23 
         if (li.USE = 3)     addtogroup = 3              
         if (li.USE = 2,3,4) addtogroup = 4                            
         
         ; create the link exclusion classes -- value toll links for passenger vehicles
         if (lw.tollclass >= firstvalue && li.TOLL@token_period@_DA > 0) addtogroup = 11
         if (lw.tollclass >= firstvalue && li.TOLL@token_period@_S2 > 0) addtogroup = 12
         if (lw.tollclass >= firstvalue && li.TOLL@token_period@_S3 > 0) addtogroup = 13
         
         ; create the link exclusion classes -- value toll links for commercial vehicles (use SML for very small, small, and medium)
         if (lw.tollclass >= firstvalue && li.TOLL@token_period@_SML > 0) addtogroup = 14
         if (lw.tollclass >= firstvalue && li.TOLL@token_period@_LRG > 0) addtogroup = 15
    
      endphase
  
      ; set the pathload parameters
      phase = iloop
      
         ; no value toll paying classes firsth
     
         ; drive alone, no value toll, exclude drive alone value toll and HOV facilities, internal demand plus intra-regional demand plus air passenger demand
         pathload path = lw.gencost_da, excludegrp = 11, 23, vol[1] = mi.1.da + mi.2.da + mi.4.da,
           MW[1]=mi.1.da + mi.2.da + mi.4.da, selectlink=(L=@token_link@), VOL[11]=MW[1]
         
         ; shared ride 2, no value toll, exclude sr2 value toll and HOV 3+ facilities, internal demand plus intra-regional demand plus air passenger demand
         pathload path = lw.gencost_s2, excludegrp = 12, 3, vol[2] =  mi.1.sr2 / 2 + mi.2.sr2 + mi.4.sr2,
           MW[2]=mi.1.sr2 / 2 + mi.2.sr2 + mi.4.sr2, selectlink=(L=@token_link@), VOL[12]=MW[2]
         
         ; shared ride 3+, no value toll, exclude sr3 value toll facilities, internal demand plus intra-regional demand plus air passenger demand
         pathload path = lw.gencost_s3, excludegrp = 13, vol[3] = mi.1.sr3 / 3.25 + mi.2.sr3 + mi.4.sr3,
           MW[3]=mi.1.sr3 / 3.25 + mi.2.sr3 + mi.4.sr3, selectlink=(L=@token_link@), VOL[13]=MW[3]
         
         ; very small, small, and medium trucks, no value toll, exclude small truck value toll facilities and HOV facilities
         pathload path = lw.gencost_sml, excludegrp = 14, 23, vol[4] = mi.3.vstruck + mi.3.struck + mi.3.mtruck,
           MW[4]=mi.3.vstruck + mi.3.struck + mi.3.mtruck, selectlink=(L=@token_link@), VOL[14]=MW[4]
         
         ; large trucks, no value toll, exclude large truck toll facilities, HOV facilities, and no commercial truck facilities 
         pathload path = lw.gencost_lrg, excludegrp = 15, 23, 4, vol[5] = mi.3.ctruck,
            MW[5]=mi.3.ctruck,, selectlink=(L=@token_link@), VOL[15]=MW[5]
         ; value toll paying classes second
         
         ; drive alone, value toll paying, exclude HOV facilities, internal demand plus intra-regional demand plus air passenger demand
         pathload path = lw.gencost_da, excludegrp = 23, vol[6] = mi.1.datoll + mi.2.datoll + mi.4.datoll,
           mw[6]=mi.1.datoll + mi.2.datoll + mi.4.datoll, selectlink=(L=@token_link@), VOL[16]=MW[6]
         
         ; shared ride 2, value toll paying, exclude HOV 3+ facilities, internal demand plus intra-regional demand plus air passenger demand
         pathload path = lw.gencost_s2, excludegrp = 3, vol[7] = mi.1.sr2toll / 2 + mi.2.sr2toll + mi.4.sr2toll,
           mw[7]= mi.1.sr2toll / 2 + mi.2.sr2toll + mi.4.sr2toll, selectlink=(L=@token_link@), VOL[17]=MW[7]
         
         ; shared ride 3+, value toll paying, no exclusions, internal demand plus intra-regional demand plus air passenger demand
         pathload path = lw.gencost_s3, vol[8] = mi.1.sr3toll / 3.25 + mi.2.sr3toll + mi.4.sr3toll,
           mw[8]= mi.1.sr3toll / 3.25 + mi.2.sr3toll + mi.4.sr3toll, selectlink=(L=@token_link@), VOL[18]=MW[8]
         
         ; very small, small, and medium trucks, value toll paying, excluded from HOV facilities, use the small truck generalized cost
         pathload path = lw.gencost_sml, excludegrp = 23, vol[9]= mi.3.vstrucktoll + mi.3.strucktoll + mi.3.mtrucktoll,
           mw[9]= mi.3.vstrucktoll + mi.3.strucktoll + mi.3.mtrucktoll, selectlink=(L=@token_link@), VOL[19]=MW[9]

         ; large trucks, value toll paying, excluded from HOV facilities and no commercial truck facilities
         pathload path = lw.gencost_lrg, excludegrp = 23, 4, vol[10] = mi.3.ctrucktoll,
           mw[10]= mi.3.ctrucktoll,, selectlink=(L=@token_link@), VOL[20]=MW[10]

      endphase  
    
      ; set adjust parameters (volume delay functions)
      phase = adjust
 
         ; very small, small, and medium trucks have a passenger car equivalent of, on average 1.0, large trucks see HwyParam.block for PCE
         function v = vol[1] + vol[2] + vol[3] + vol[4] + trkpce*vol[5] + vol[6] + vol[7] + vol[8] + vol[9] + trkpce*vol[10]
           
         ; the volume delay functions are housed in the SpeedFlowCurve.block file
         read file = ctramp\scripts\block\SpeedFlowCurve.block 
      
         ; set the generalized cost functions (same as before, only now we start with "time")
         lw.gencost_da = time + (0.6/vot) * (lw.dist*li.autoopc + li.TOLL@token_period@_DA)
         lw.gencost_s2 = time + (0.6/vot) * (lw.dist*li.autoopc + li.TOLL@token_period@_S2 / sr2costshare)  + @token_hov_xfer_pen@
         lw.gencost_s3 = time + (0.6/vot) * (lw.dist*li.autoopc + li.TOLL@token_period@_S3 / sr3costshare) + @token_hov_xfer_pen@
         
         lw.gencost_sml = time + (0.6/truckvot) * (lw.dist*li.smtropc + li.TOLL@token_period@_SML)
         lw.gencost_lrg = time + (0.6/truckvot) * (lw.dist*li.smtropc + li.TOLL@token_period@_LRG)
         
      endphase
      
   endrun      

  run PGM=matrix
   filei mati     ="hwy\selectlink\load@token_period@_selectlink_@token_link@_ODs_v2.mat"
   fileo printo[1]="hwy\selectlink\load@token_period@_selectlink_@token_link@_ODs_v2.txt"

   fillmw mw[1]=mi.1.1(10)  ; set M@[1] = mi.1.1, MW[2] = mi.1.2, etc
   MW[11] = MW[1]+MW[2]+MW[3]+MW[4]+MW[5]+MW[6]+MW[7]+MW[8]+MW[9]+MW[10]  ; all vehicles
   MW[12] = MW[1]+MW[2]+MW[3]            +MW[6]+MW[7]+MW[8]               ; passenger vehicles

   if (I==1) print CSV=T, LIST="OTAZ","DTAZ","vol","vol_pax","vol_da","vol_s2","vol_s3","vol_sm","vol_hv","vol_daT","vol_s2T","vol_s3T","vol_smT","vol_hvT", printo=1
   JLOOP
     IF (MW[11] > 0.01) print CSV=T, LIST=I(4L),J(4L),MW[11](10.2L),MW[12](10.2L),Mw[1](10.2L),mw[2](10.2L),mw[3](10.2L),mw[4](10.2L),mw[5](10.2L),mw[6](10.2L),mw[7](10.2L),mw[8](10.2L),mw[9](10.2L),mw[10](10.2L), printo=1
   ENDJLOOP

   endrun
   EndDistributeMultistep
  
endloop ; time period loop

; Wait4Files files = CTRAMP1.script.end, CTRAMP2.script.end, CTRAMP3.script.end, CTRAMP4.script.end, CTRAMP5.script.end, 
;          PrintFiles = merge, DelDistribFiles = t, CheckReturnCode = t
Wait4Files files = CTRAMP2.script.end, PrintFiles = merge, DelDistribFiles = t, CheckReturnCode = t