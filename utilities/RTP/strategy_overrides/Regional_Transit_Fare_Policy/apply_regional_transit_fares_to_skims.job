;
; Apply Regional Transit Fares (distance-based, applies to a subset of modes)
; to Transit skims
;
; This is part of Strategy T2: Improve the Rider Experience through Transit Network Integration
;

token_model_dir = '%MODEL_DIR%'
token_trnassigniter = '%TRNASSIGNITER%'

; time period loop
loop period = 1,5
  if (period = 1)   
     token_period   = 'ea'
  elseif (period = 2)
     token_period   = 'am'
  elseif (period = 3)
     token_period   = 'md'
  elseif (period = 4)
     token_period   = 'pm'
  elseif (period = 5)
     token_period   = 'ev'
  endif

  ; access/egress loop (walk/walk, auto/walk, walk/auto)
  loop accegg = 1,3

    if (accegg = 1)         
      token_access = 'wlk'
      token_egress = 'wlk'
      token_boardpen = 'factor boardpen = 0, 20, 45, 50, 60'
    elseif (accegg = 2)        
      token_access = 'drv'
      token_egress = 'wlk'
      token_boardpen = 'factor boardpen = 0, 30, 45, 50, 60'
    elseif (accegg = 3)       
      token_access = 'wlk'
      token_egress = 'drv'
      token_boardpen = 'factor boardpen = 0, 30, 45, 50, 60'
    endif

    counter = 3 * (period - 1) + accegg

    ; distribute the tasks
    DistributeMultistep processid = "ctramp", processnum = counter,  commpath = '%COMMPATH%'

      ; line haul loop
      loop path = 1,6
        if (path = 1)
          token_path = 'com'
        elseif (path = 2)
          token_path = 'hvy'
        elseif (path = 3)
          token_path = 'exp'
        elseif (path = 4)
          token_path = 'lrf'
        elseif (path = 5)
          token_path = 'loc'
        elseif (path = 6)
          token_path = 'trn'
        endif

        ; rename original (e.g. trnskmam_wlk_com_wlk.avg.iter0.tpp => trnskmam_wlk_com_wlk_woRegionalFare.tpp)
        * move trnskm@token_period@_@token_access@_@token_path@_@token_egress@.avg.iter@token_trnassigniter@.tpp trnskm@token_period@_@token_access@_@token_path@_@token_egress@_woRegionalFare.tpp

        ; create version with regional fares
        RUN PGM=MATRIX
          FILEI MATI[1]=trnskm@token_period@_@token_access@_@token_path@_@token_egress@_woRegionalFare.tpp
          FILEO MATO[1]=trnskm@token_period@_@token_access@_@token_path@_@token_egress@.avg.iter@token_trnassigniter@.tpp, MO=1-27,
                NAME=ivt, iwait, xwait, wait, wacc, waux, wegr, dtime, ddist, fare, boards,
                     ivtLOC,  ivtLRF,  ivtEXP,  ivtHVY,  ivtCOM,  ivtFerry,  ivtMUNILoc, ivtMUNIMet,
                     distLOC, distLRF, distEXP, distHVY, distCOM, distFerry, firstMode, distRegFar
          
          ; copy over existing
          FILLMW MW[1] = MI.1.1(27)

          ; total distance on all transit modes
          MW[30] = MI.1.distLOC + MI.1.distLRF + MI.1.distEXP + MI.1.distHVY + MI.1.distCOM

          JLOOP
            ; for modes with distance in regional fare modes, apply distance-based fares
            IF ((MI.1.distRegFar > 0) & (MW[30] < 1000))
              MW[10] = 217
            ELSEIF ((MI.1.distRegFar > 0) & (MW[30] < 2000))
              MW[10] = 280
            ELSEIF ((MI.1.distRegFar > 0) & (MW[30] < 3000))
              MW[10] = 342
            ELSEIF ((MI.1.distRegFar > 0) & (MW[30] < 4000))
              MW[10] = 404
            ELSEIF ((MI.1.distRegFar > 0) & (MW[30] < 5000))
              MW[10] = 466
            ELSEIF (MI.1.distRegFar > 0)
              MW[10] = 528
            ENDIF
          ENDJLOOP
        ENDRUN ; PGM MATRIX

      endloop ; path loop
    EndDistributeMultistep
  endloop ; access/egress
endloop ; time period loop

wait4files files = ctramp1.script.end,  ctramp2.script.end,  ctramp3.script.end,  ctramp4.script.end,  ctramp5.script.end,
                   ctramp6.script.end,  ctramp7.script.end,  ctramp8.script.end,  ctramp9.script.end,  ctramp10.script.end,
                   ctramp11.script.end, ctramp12.script.end, ctramp13.script.end, ctramp14.script.end, ctramp15.script.end, 
          printfiles = save, DelDistribFiles = t, CheckReturnCode = t

