; Implementation of BuildAirPaxMatrices for Scenario B-1 of the RASPA Study (http://mtc.ca.gov/planning/air_plan/RASPA-2011_update/Volume_2-Major_Reports.pdf)
; ----------------------------------------------------------------------------------------------------------------
;
; BuildAirPaxMatrices.job
;
; TP+ script that creates air passenger vehicle trip tables for the Bay Area's three major airports, namely SFO,
; OAK, and SJC.  Geoff Gosling, a consultant, created vehicle trip tables segmented by time of day, travel mode,
; and access/egress direction (i.e. to the airport or from the airport) for years 2007 and 2035.  The tables are
; based on a 2006 Air Passenger survey, which was conducted at SFO and OAK (but not SJC).  The five time
; periods correspond to the time periods used by the travel model and are as follows: (a) early AM, before 6 am; 
; (b) AM peak period, 6 am to 10 am; (c) midday, 10 am to 3 pm; (d) PM peak period, 3 pm to 7 pm; and, (e) evening, 
; after 7 pm.  The travel modes are as follows: (a) escort (drive alone, shared ride 2, and shared ride 3+); (b) 
; park (da, sr2, & sr3+); (c) rental car (da, sr2, & sr3+); (d) taxi ((da, sr2, & sr3+); (e) limo (da, sr2, & sr3+);
; (f) shared ride van (all assumed to be sr3); (g) hotel shuttle (all assumed to be sr3); and, (h) charter bus (all
; assumed to be sr3).  The shared ride van, hotel shuttle, and charter bus modes are assumed to have no deadhead travel. 
; The return escort trip is included, as are the deadhead limo and taxi trips. 
;
;
; The script reads in DBF files crated from Mr. Gosling's Excel files for 2007 and 2035, and uses a simple linear
; interpolation to create versions for years other than 2007 and 2035, including 2015, 2020, 2025, 2030, 2040,
; 2045 and 2050.
;
; These DBFs are then converted into Cube matrices, with optional variants accounting for airport closures and diverting
; trips to alternative airports for Horizon Future scenarios.  Finally, a highway-assignment ready matrix file is
; created for each time-of-day interval.  Here, we assume that no air passengers use HOT lanes (probably not exactly
; true in certain future year scenarios, but the assumption is made here as a simplification).
;
; Transit travel to the airports is not included in these vehicle trip tables.
;
;
; Input:  (A)  Year-, access/egress-, and airport-specific database file with 90 columns of data for each TAZ.  There
;              are 18 columns for each time-of-day interval as follows:
;            (1)   Escort, drive alone
;            (2)   Escort, shared ride 2
;            (3)   Escort, shared ride 3+
;            (4)   Park, drive alone
;            (5)   Park, shared ride 2
;            (6)   Park, shared ride 3+
;            (7)   Rental car, drive alone
;            (8)   Rental car, shared ride 2
;            (9)   Rental car, shared ride 3+
;            (10)  Taxi, drive alone
;            (11)  Taxi, shared ride 2
;            (12)  Taxi, shared ride 3+
;            (13)  Limo, drive alone
;            (14)  Limo, shared ride 2
;            (15)  Limo, shared ride 3+
;            (16)  Shared ride van, shared ride 3+
;            (17)  Hotel shuttle, shared ride 3+
;            (18)  Charter bus, shared ride 3+
;
;
; Output: (A) For each forecast year (2007, 2015, 2020, 2025, 2030, 2035, and 2040), five time-of-day-specific
;             tables, each containing origin/destination vehicle matrices for the following modes:
;           (1) drive alone (DA)
;           (2) shared ride 2 (SR2)
;           (3) shared ride 3+ (SR3)
;           (4) drive alone and willing to pay a value toll (DATOLL)
;           (5) shared ride 2 and willing to pay a value toll (SR2TOLL)
;           (6) shared ride 3+ and willing to pay a value toll (SR3TOLL)
;
; Notes:  (1)
;
; See also: (1) HwyAssign.job, which performs highway assignments; 
;
; version:  Travel Model One (v 0.3)
; authors:  dto (2011 12 14)
;
;
; ----------------------------------------------------------------------------------------------------------------
; STEP 1
; The script reads in DBF files crated from Mr. Gosling's Excel files for 2007 and 2035, and uses a simple linear
; interpolation to create versions for years other than 2007 and 2035, including 2015, 2020, 2025, 2030, 2040,
; 2045 and 2050.
; ----------------------------------------------------------------------------------------------------------------
loop fYear = 1,7

   if (fYear = 1) 
   
      token_year  = '2015b'
      token_scale = '(2015 - 2007)/(2035 - 2007)'
      
   elseif (fYear = 2)
   
      token_year  = '2020b'
      token_scale = '(2020 - 2007)/(2035 - 2007)'
      
   elseif (fYear = 3)
   
      token_year  = '2025b'
      token_scale = '(2025 - 2007)/(2035 - 2007)'
      
   elseif (fYear = 4)
   
      token_year  = '2030b'
      token_scale = '(2030 - 2007)/(2035 - 2007)'
      
   elseif (fYear = 5)
   
      token_year  = '2040b'
      token_scale = '(2040 - 2007)/(2035 - 2007)'

   elseif (fYear = 6)
   
      token_year  = '2045b'
      token_scale = '(2045 - 2007)/(2035 - 2007)'

   elseif (fYear = 7)
   
      token_year  = '2050b'
      token_scale = '(2050 - 2007)/(2035 - 2007)'

   endif

   loop airport = 1,3
   
      if (airport = 1)
         token_airport = 'SFO'
         airport_taz   = '239'
      endif

      if (airport = 2)
         token_airport = 'OAK'
         airport_taz   = '874'
      endif

      if (airport = 3)
         token_airport = 'SJC'
         airport_taz   = '434'
      endif

      loop toFrom = 1,2
      
         if (toFrom = 1) token_toFrom = 'to'
         if (toFrom = 2) token_toFrom = 'from'

         run pgm = matrix
         
            filei RECI   = .\GoslingSummaries\2007_@token_toFrom@@token_airport@.dbf
            filei DBI[2] = .\GoslingSummaries\2035b_@token_toFrom@@token_airport@.dbf

            fileo RECO[1] = .\GoslingScaled\@token_year@_@token_toFrom@@token_airport@.dbf, fields=RECI.ALLFIELDS

            ; read 1 record from DBI[2]
            x = DBIReadNext(2,1)

            print list=x, DI.2.ORIG, DI.2.DEST, RI.ORIG, RI.DEST

            IF (DI.2.ORIG != RI.ORIG) ABORT MSG = "MISMATCH ORIG records in 2007 vs 2035b Gosling DBF Input"
            IF (DI.2.DEST != RI.DEST) ABORT MSG = "MISMATCH DEST records in 2007 vs 2035b Gosling DBF Input"

            RO.ORIG = RI.ORIG
            RO.DEST = RI.DEST

            ; Early AM
            RO.EA_ES_DA = RI.EA_ES_DA + @token_scale@*(DI.2.EA_ES_DA - RI.EA_ES_DA)
            RO.EA_ES_S2 = RI.EA_ES_S2 + @token_scale@*(DI.2.EA_ES_S2 - RI.EA_ES_S2)
            RO.EA_ES_S3 = RI.EA_ES_S3 + @token_scale@*(DI.2.EA_ES_S3 - RI.EA_ES_S3)
            RO.EA_PK_DA = RI.EA_PK_DA + @token_scale@*(DI.2.EA_PK_DA - RI.EA_PK_DA)
            RO.EA_PK_S2 = RI.EA_PK_S2 + @token_scale@*(DI.2.EA_PK_S2 - RI.EA_PK_S2)
            RO.EA_PK_S3 = RI.EA_PK_S3 + @token_scale@*(DI.2.EA_PK_S3 - RI.EA_PK_S3)
            RO.EA_RN_DA = RI.EA_RN_DA + @token_scale@*(DI.2.EA_RN_DA - RI.EA_RN_DA)
            RO.EA_RN_S2 = RI.EA_RN_S2 + @token_scale@*(DI.2.EA_RN_S2 - RI.EA_RN_S2)
            RO.EA_RN_S3 = RI.EA_RN_S3 + @token_scale@*(DI.2.EA_RN_S3 - RI.EA_RN_S3)
            RO.EA_TX_DA = RI.EA_TX_DA + @token_scale@*(DI.2.EA_TX_DA - RI.EA_TX_DA)
            RO.EA_TX_S2 = RI.EA_TX_S2 + @token_scale@*(DI.2.EA_TX_S2 - RI.EA_TX_S2)
            RO.EA_TX_S3 = RI.EA_TX_S3 + @token_scale@*(DI.2.EA_TX_S3 - RI.EA_TX_S3)
            RO.EA_LI_DA = RI.EA_LI_DA + @token_scale@*(DI.2.EA_LI_DA - RI.EA_LI_DA)
            RO.EA_LI_S2 = RI.EA_LI_S2 + @token_scale@*(DI.2.EA_LI_S2 - RI.EA_LI_S2)
            RO.EA_LI_S3 = RI.EA_LI_S3 + @token_scale@*(DI.2.EA_LI_S3 - RI.EA_LI_S3)
            RO.EA_VN_S3 = RI.EA_VN_S3 + @token_scale@*(DI.2.EA_VN_S3 - RI.EA_VN_S3)
            RO.EA_HT_S3 = RI.EA_HT_S3 + @token_scale@*(DI.2.EA_HT_S3 - RI.EA_HT_S3)
            RO.EA_CH_S3 = RI.EA_CH_S3 + @token_scale@*(DI.2.EA_CH_S3 - RI.EA_CH_S3)

            ; AM
            RO.AM_ES_DA = RI.AM_ES_DA + @token_scale@*(DI.2.AM_ES_DA - RI.AM_ES_DA)
            RO.AM_ES_S2 = RI.AM_ES_S2 + @token_scale@*(DI.2.AM_ES_S2 - RI.AM_ES_S2)
            RO.AM_ES_S3 = RI.AM_ES_S3 + @token_scale@*(DI.2.AM_ES_S3 - RI.AM_ES_S3)
            RO.AM_PK_DA = RI.AM_PK_DA + @token_scale@*(DI.2.AM_PK_DA - RI.AM_PK_DA)
            RO.AM_PK_S2 = RI.AM_PK_S2 + @token_scale@*(DI.2.AM_PK_S2 - RI.AM_PK_S2)
            RO.AM_PK_S3 = RI.AM_PK_S3 + @token_scale@*(DI.2.AM_PK_S3 - RI.AM_PK_S3)
            RO.AM_RN_DA = RI.AM_RN_DA + @token_scale@*(DI.2.AM_RN_DA - RI.AM_RN_DA)
            RO.AM_RN_S2 = RI.AM_RN_S2 + @token_scale@*(DI.2.AM_RN_S2 - RI.AM_RN_S2)
            RO.AM_RN_S3 = RI.AM_RN_S3 + @token_scale@*(DI.2.AM_RN_S3 - RI.AM_RN_S3)
            RO.AM_TX_DA = RI.AM_TX_DA + @token_scale@*(DI.2.AM_TX_DA - RI.AM_TX_DA)
            RO.AM_TX_S2 = RI.AM_TX_S2 + @token_scale@*(DI.2.AM_TX_S2 - RI.AM_TX_S2)
            RO.AM_TX_S3 = RI.AM_TX_S3 + @token_scale@*(DI.2.AM_TX_S3 - RI.AM_TX_S3)
            RO.AM_LI_DA = RI.AM_LI_DA + @token_scale@*(DI.2.AM_LI_DA - RI.AM_LI_DA)
            RO.AM_LI_S2 = RI.AM_LI_S2 + @token_scale@*(DI.2.AM_LI_S2 - RI.AM_LI_S2)
            RO.AM_LI_S3 = RI.AM_LI_S3 + @token_scale@*(DI.2.AM_LI_S3 - RI.AM_LI_S3)
            RO.AM_VN_S3 = RI.AM_VN_S3 + @token_scale@*(DI.2.AM_VN_S3 - RI.AM_VN_S3)
            RO.AM_HT_S3 = RI.AM_HT_S3 + @token_scale@*(DI.2.AM_HT_S3 - RI.AM_HT_S3)
            RO.AM_CH_S3 = RI.AM_CH_S3 + @token_scale@*(DI.2.AM_CH_S3 - RI.AM_CH_S3)

            ; Midday
            RO.MD_ES_DA = RI.MD_ES_DA + @token_scale@*(DI.2.MD_ES_DA - RI.MD_ES_DA)
            RO.MD_ES_S2 = RI.MD_ES_S2 + @token_scale@*(DI.2.MD_ES_S2 - RI.MD_ES_S2)
            RO.MD_ES_S3 = RI.MD_ES_S3 + @token_scale@*(DI.2.MD_ES_S3 - RI.MD_ES_S3)
            RO.MD_PK_DA = RI.MD_PK_DA + @token_scale@*(DI.2.MD_PK_DA - RI.MD_PK_DA)
            RO.MD_PK_S2 = RI.MD_PK_S2 + @token_scale@*(DI.2.MD_PK_S2 - RI.MD_PK_S2)
            RO.MD_PK_S3 = RI.MD_PK_S3 + @token_scale@*(DI.2.MD_PK_S3 - RI.MD_PK_S3)
            RO.MD_RN_DA = RI.MD_RN_DA + @token_scale@*(DI.2.MD_RN_DA - RI.MD_RN_DA)
            RO.MD_RN_S2 = RI.MD_RN_S2 + @token_scale@*(DI.2.MD_RN_S2 - RI.MD_RN_S2)
            RO.MD_RN_S3 = RI.MD_RN_S3 + @token_scale@*(DI.2.MD_RN_S3 - RI.MD_RN_S3)
            RO.MD_TX_DA = RI.MD_TX_DA + @token_scale@*(DI.2.MD_TX_DA - RI.MD_TX_DA)
            RO.MD_TX_S2 = RI.MD_TX_S2 + @token_scale@*(DI.2.MD_TX_S2 - RI.MD_TX_S2)
            RO.MD_TX_S3 = RI.MD_TX_S3 + @token_scale@*(DI.2.MD_TX_S3 - RI.MD_TX_S3)
            RO.MD_LI_DA = RI.MD_LI_DA + @token_scale@*(DI.2.MD_LI_DA - RI.MD_LI_DA)
            RO.MD_LI_S2 = RI.MD_LI_S2 + @token_scale@*(DI.2.MD_LI_S2 - RI.MD_LI_S2)
            RO.MD_LI_S3 = RI.MD_LI_S3 + @token_scale@*(DI.2.MD_LI_S3 - RI.MD_LI_S3)
            RO.MD_VN_S3 = RI.MD_VN_S3 + @token_scale@*(DI.2.MD_VN_S3 - RI.MD_VN_S3)
            RO.MD_HT_S3 = RI.MD_HT_S3 + @token_scale@*(DI.2.MD_HT_S3 - RI.MD_HT_S3)
            RO.MD_CH_S3 = RI.MD_CH_S3 + @token_scale@*(DI.2.MD_CH_S3 - RI.MD_CH_S3)

            ; PM
            RO.PM_ES_DA = RI.PM_ES_DA + @token_scale@*(DI.2.PM_ES_DA - RI.PM_ES_DA)
            RO.PM_ES_S2 = RI.PM_ES_S2 + @token_scale@*(DI.2.PM_ES_S2 - RI.PM_ES_S2)
            RO.PM_ES_S3 = RI.PM_ES_S3 + @token_scale@*(DI.2.PM_ES_S3 - RI.PM_ES_S3)
            RO.PM_PK_DA = RI.PM_PK_DA + @token_scale@*(DI.2.PM_PK_DA - RI.PM_PK_DA)
            RO.PM_PK_S2 = RI.PM_PK_S2 + @token_scale@*(DI.2.PM_PK_S2 - RI.PM_PK_S2)
            RO.PM_PK_S3 = RI.PM_PK_S3 + @token_scale@*(DI.2.PM_PK_S3 - RI.PM_PK_S3)
            RO.PM_RN_DA = RI.PM_RN_DA + @token_scale@*(DI.2.PM_RN_DA - RI.PM_RN_DA)
            RO.PM_RN_S2 = RI.PM_RN_S2 + @token_scale@*(DI.2.PM_RN_S2 - RI.PM_RN_S2)
            RO.PM_RN_S3 = RI.PM_RN_S3 + @token_scale@*(DI.2.PM_RN_S3 - RI.PM_RN_S3)
            RO.PM_TX_DA = RI.PM_TX_DA + @token_scale@*(DI.2.PM_TX_DA - RI.PM_TX_DA)
            RO.PM_TX_S2 = RI.PM_TX_S2 + @token_scale@*(DI.2.PM_TX_S2 - RI.PM_TX_S2)
            RO.PM_TX_S3 = RI.PM_TX_S3 + @token_scale@*(DI.2.PM_TX_S3 - RI.PM_TX_S3)
            RO.PM_LI_DA = RI.PM_LI_DA + @token_scale@*(DI.2.PM_LI_DA - RI.PM_LI_DA)
            RO.PM_LI_S2 = RI.PM_LI_S2 + @token_scale@*(DI.2.PM_LI_S2 - RI.PM_LI_S2)
            RO.PM_LI_S3 = RI.PM_LI_S3 + @token_scale@*(DI.2.PM_LI_S3 - RI.PM_LI_S3)
            RO.PM_VN_S3 = RI.PM_VN_S3 + @token_scale@*(DI.2.PM_VN_S3 - RI.PM_VN_S3)
            RO.PM_HT_S3 = RI.PM_HT_S3 + @token_scale@*(DI.2.PM_HT_S3 - RI.PM_HT_S3)
            RO.PM_CH_S3 = RI.PM_CH_S3 + @token_scale@*(DI.2.PM_CH_S3 - RI.PM_CH_S3)

            ; EV
            RO.EV_ES_DA = RI.EV_ES_DA + @token_scale@*(DI.2.EV_ES_DA - RI.EV_ES_DA)
            RO.EV_ES_S2 = RI.EV_ES_S2 + @token_scale@*(DI.2.EV_ES_S2 - RI.EV_ES_S2)
            RO.EV_ES_S3 = RI.EV_ES_S3 + @token_scale@*(DI.2.EV_ES_S3 - RI.EV_ES_S3)
            RO.EV_PK_DA = RI.EV_PK_DA + @token_scale@*(DI.2.EV_PK_DA - RI.EV_PK_DA)
            RO.EV_PK_S2 = RI.EV_PK_S2 + @token_scale@*(DI.2.EV_PK_S2 - RI.EV_PK_S2)
            RO.EV_PK_S3 = RI.EV_PK_S3 + @token_scale@*(DI.2.EV_PK_S3 - RI.EV_PK_S3)
            RO.EV_RN_DA = RI.EV_RN_DA + @token_scale@*(DI.2.EV_RN_DA - RI.EV_RN_DA)
            RO.EV_RN_S2 = RI.EV_RN_S2 + @token_scale@*(DI.2.EV_RN_S2 - RI.EV_RN_S2)
            RO.EV_RN_S3 = RI.EV_RN_S3 + @token_scale@*(DI.2.EV_RN_S3 - RI.EV_RN_S3)
            RO.EV_TX_DA = RI.EV_TX_DA + @token_scale@*(DI.2.EV_TX_DA - RI.EV_TX_DA)
            RO.EV_TX_S2 = RI.EV_TX_S2 + @token_scale@*(DI.2.EV_TX_S2 - RI.EV_TX_S2)
            RO.EV_TX_S3 = RI.EV_TX_S3 + @token_scale@*(DI.2.EV_TX_S3 - RI.EV_TX_S3)
            RO.EV_LI_DA = RI.EV_LI_DA + @token_scale@*(DI.2.EV_LI_DA - RI.EV_LI_DA)
            RO.EV_LI_S2 = RI.EV_LI_S2 + @token_scale@*(DI.2.EV_LI_S2 - RI.EV_LI_S2)
            RO.EV_LI_S3 = RI.EV_LI_S3 + @token_scale@*(DI.2.EV_LI_S3 - RI.EV_LI_S3)
            RO.EV_VN_S3 = RI.EV_VN_S3 + @token_scale@*(DI.2.EV_VN_S3 - RI.EV_VN_S3)
            RO.EV_HT_S3 = RI.EV_HT_S3 + @token_scale@*(DI.2.EV_HT_S3 - RI.EV_HT_S3)
            RO.EV_CH_S3 = RI.EV_CH_S3 + @token_scale@*(DI.2.EV_CH_S3 - RI.EV_CH_S3)

            WRITE RECO=1

         endrun

      endloop ; toFrom

      ; ----------------------------------------------------------------------------------------------------------------
      ; STEP 2
      ; These DBFs are then converted into Cube matrices, with optional variants accounting for airport closures and
      ; diverting trips to alternative airports for Horizon Future scenarios.  
      ; ----------------------------------------------------------------------------------------------------------------
      run pgm = matrix
         
         filei ZDATI[1] = .\GoslingScaled\@token_year@_to@token_airport@.dbf,   Z=ORIG
         filei ZDATI[2] = .\GoslingScaled\@token_year@_from@token_airport@.dbf, Z=DEST

         fileo mato[1] = @token_year@_to@token_airport@.tpp, mo = 1-90, name = 
            EA_Escort_DA,EA_Escort_S2,EA_Escort_S3,EA_Park_DA,EA_Park_S2,EA_Park_S3,EA_Rent_DA,EA_Rent_S2,EA_Rent_S3,EA_Taxi_DA,EA_Taxi_S2,EA_Taxi_S3,EA_Limo_DA,EA_Limo_S2,EA_Limo_S3,EA_Van_S3,EA_Hotel_S3,EA_Chart_S3,
            AM_Escort_DA,AM_Escort_S2,AM_Escort_S3,AM_Park_DA,AM_Park_S2,AM_Park_S3,AM_Rent_DA,AM_Rent_S2,AM_Rent_S3,AM_Taxi_DA,AM_Taxi_S2,AM_Taxi_S3,AM_Limo_DA,AM_Limo_S2,AM_Limo_S3,AM_Van_S3,AM_Hotel_S3,AM_Chart_S3,
            MD_Escort_DA,MD_Escort_S2,MD_Escort_S3,MD_Park_DA,MD_Park_S2,MD_Park_S3,MD_Rent_DA,MD_Rent_S2,MD_Rent_S3,MD_Taxi_DA,MD_Taxi_S2,MD_Taxi_S3,MD_Limo_DA,MD_Limo_S2,MD_Limo_S3,MD_Van_S3,MD_Hotel_S3,MD_Chart_S3,
            PM_Escort_DA,PM_Escort_S2,PM_Escort_S3,PM_Park_DA,PM_Park_S2,PM_Park_S3,PM_Rent_DA,PM_Rent_S2,PM_Rent_S3,PM_Taxi_DA,PM_Taxi_S2,PM_Taxi_S3,PM_Limo_DA,PM_Limo_S2,PM_Limo_S3,PM_Van_S3,PM_Hotel_S3,PM_Chart_S3,
            EV_Escort_DA,EV_Escort_S2,EV_Escort_S3,EV_Park_DA,EV_Park_S2,EV_Park_S3,EV_Rent_DA,EV_Rent_S2,EV_Rent_S3,EV_Taxi_DA,EV_Taxi_S2,EV_Taxi_S3,EV_Limo_DA,EV_Limo_S2,EV_Limo_S3,EV_Van_S3,EV_Hotel_S3,EV_Chart_S3

         ; note this is the transpose
         fileo mato[2] = @token_year@_from@token_airport@_t.tpp, mo = 101-190, name = 
            EA_Escort_DA,EA_Escort_S2,EA_Escort_S3,EA_Park_DA,EA_Park_S2,EA_Park_S3,EA_Rent_DA,EA_Rent_S2,EA_Rent_S3,EA_Taxi_DA,EA_Taxi_S2,EA_Taxi_S3,EA_Limo_DA,EA_Limo_S2,EA_Limo_S3,EA_Van_S3,EA_Hotel_S3,EA_Chart_S3,
            AM_Escort_DA,AM_Escort_S2,AM_Escort_S3,AM_Park_DA,AM_Park_S2,AM_Park_S3,AM_Rent_DA,AM_Rent_S2,AM_Rent_S3,AM_Taxi_DA,AM_Taxi_S2,AM_Taxi_S3,AM_Limo_DA,AM_Limo_S2,AM_Limo_S3,AM_Van_S3,AM_Hotel_S3,AM_Chart_S3,
            MD_Escort_DA,MD_Escort_S2,MD_Escort_S3,MD_Park_DA,MD_Park_S2,MD_Park_S3,MD_Rent_DA,MD_Rent_S2,MD_Rent_S3,MD_Taxi_DA,MD_Taxi_S2,MD_Taxi_S3,MD_Limo_DA,MD_Limo_S2,MD_Limo_S3,MD_Van_S3,MD_Hotel_S3,MD_Chart_S3,
            PM_Escort_DA,PM_Escort_S2,PM_Escort_S3,PM_Park_DA,PM_Park_S2,PM_Park_S3,PM_Rent_DA,PM_Rent_S2,PM_Rent_S3,PM_Taxi_DA,PM_Taxi_S2,PM_Taxi_S3,PM_Limo_DA,PM_Limo_S2,PM_Limo_S3,PM_Van_S3,PM_Hotel_S3,PM_Chart_S3,
            EV_Escort_DA,EV_Escort_S2,EV_Escort_S3,EV_Park_DA,EV_Park_S2,EV_Park_S3,EV_Rent_DA,EV_Rent_S2,EV_Rent_S3,EV_Taxi_DA,EV_Taxi_S2,EV_Taxi_S3,EV_Limo_DA,EV_Limo_S2,EV_Limo_S3,EV_Van_S3,EV_Hotel_S3,EV_Chart_S3

         zones = 1454

         ; destination is @airport_taz@

         ; ==================== EA ====================
         ; Escort
         MW[  1] = ZI.1.EA_ES_DA INCLUDE=@airport_taz@
         MW[  2] = ZI.1.EA_ES_S2 INCLUDE=@airport_taz@
         MW[  3] = ZI.1.EA_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[  4] = ZI.1.EA_PK_DA INCLUDE=@airport_taz@
         MW[  5] = ZI.1.EA_PK_S2 INCLUDE=@airport_taz@
         MW[  6] = ZI.1.EA_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[  7] = ZI.1.EA_RN_DA INCLUDE=@airport_taz@
         MW[  8] = ZI.1.EA_RN_S2 INCLUDE=@airport_taz@
         MW[  9] = ZI.1.EA_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[ 10] = ZI.1.EA_TX_DA INCLUDE=@airport_taz@
         MW[ 11] = ZI.1.EA_TX_S2 INCLUDE=@airport_taz@
         MW[ 12] = ZI.1.EA_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[ 13] = ZI.1.EA_LI_DA INCLUDE=@airport_taz@
         MW[ 14] = ZI.1.EA_LI_S2 INCLUDE=@airport_taz@
         MW[ 15] = ZI.1.EA_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[ 16] = ZI.1.EA_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[ 17] = ZI.1.EA_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[ 18] = ZI.1.EA_CH_S3 INCLUDE=@airport_taz@

         ; ==================== AM ====================
         ; Escort
         MW[ 19] = ZI.1.AM_ES_DA INCLUDE=@airport_taz@
         MW[ 20] = ZI.1.AM_ES_S2 INCLUDE=@airport_taz@
         MW[ 21] = ZI.1.AM_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[ 22] = ZI.1.AM_PK_DA INCLUDE=@airport_taz@
         MW[ 23] = ZI.1.AM_PK_S2 INCLUDE=@airport_taz@
         MW[ 24] = ZI.1.AM_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[ 25] = ZI.1.AM_RN_DA INCLUDE=@airport_taz@
         MW[ 26] = ZI.1.AM_RN_S2 INCLUDE=@airport_taz@
         MW[ 27] = ZI.1.AM_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[ 28] = ZI.1.AM_TX_DA INCLUDE=@airport_taz@
         MW[ 29] = ZI.1.AM_TX_S2 INCLUDE=@airport_taz@
         MW[ 30] = ZI.1.AM_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[ 31] = ZI.1.AM_LI_DA INCLUDE=@airport_taz@
         MW[ 32] = ZI.1.AM_LI_S2 INCLUDE=@airport_taz@
         MW[ 33] = ZI.1.AM_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[ 34] = ZI.1.AM_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[ 35] = ZI.1.AM_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[ 36] = ZI.1.AM_CH_S3 INCLUDE=@airport_taz@

         ; ==================== MD ====================
         ; Escort
         MW[ 37] = ZI.1.MD_ES_DA INCLUDE=@airport_taz@
         MW[ 38] = ZI.1.MD_ES_S2 INCLUDE=@airport_taz@
         MW[ 39] = ZI.1.MD_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[ 40] = ZI.1.MD_PK_DA INCLUDE=@airport_taz@
         MW[ 41] = ZI.1.MD_PK_S2 INCLUDE=@airport_taz@
         MW[ 42] = ZI.1.MD_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[ 43] = ZI.1.MD_RN_DA INCLUDE=@airport_taz@
         MW[ 44] = ZI.1.MD_RN_S2 INCLUDE=@airport_taz@
         MW[ 45] = ZI.1.MD_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[ 46] = ZI.1.MD_TX_DA INCLUDE=@airport_taz@
         MW[ 47] = ZI.1.MD_TX_S2 INCLUDE=@airport_taz@
         MW[ 48] = ZI.1.MD_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[ 49] = ZI.1.MD_LI_DA INCLUDE=@airport_taz@
         MW[ 50] = ZI.1.MD_LI_S2 INCLUDE=@airport_taz@
         MW[ 51] = ZI.1.MD_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[ 52] = ZI.1.MD_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[ 53] = ZI.1.MD_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[ 54] = ZI.1.MD_CH_S3 INCLUDE=@airport_taz@

         ; ==================== PM ====================
         ; Escort
         MW[ 55] = ZI.1.PM_ES_DA INCLUDE=@airport_taz@
         MW[ 56] = ZI.1.PM_ES_S2 INCLUDE=@airport_taz@
         MW[ 57] = ZI.1.PM_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[ 58] = ZI.1.PM_PK_DA INCLUDE=@airport_taz@
         MW[ 59] = ZI.1.PM_PK_S2 INCLUDE=@airport_taz@
         MW[ 60] = ZI.1.PM_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[ 61] = ZI.1.PM_RN_DA INCLUDE=@airport_taz@
         MW[ 62] = ZI.1.PM_RN_S2 INCLUDE=@airport_taz@
         MW[ 63] = ZI.1.PM_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[ 64] = ZI.1.PM_TX_DA INCLUDE=@airport_taz@
         MW[ 65] = ZI.1.PM_TX_S2 INCLUDE=@airport_taz@
         MW[ 66] = ZI.1.PM_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[ 67] = ZI.1.PM_LI_DA INCLUDE=@airport_taz@
         MW[ 68] = ZI.1.PM_LI_S2 INCLUDE=@airport_taz@
         MW[ 69] = ZI.1.PM_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[ 70] = ZI.1.PM_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[ 71] = ZI.1.PM_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[ 72] = ZI.1.PM_CH_S3 INCLUDE=@airport_taz@

         ; ==================== EV ====================
         ; Escort
         MW[ 73] = ZI.1.EV_ES_DA INCLUDE=@airport_taz@
         MW[ 74] = ZI.1.EV_ES_S2 INCLUDE=@airport_taz@
         MW[ 75] = ZI.1.EV_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[ 76] = ZI.1.EV_PK_DA INCLUDE=@airport_taz@
         MW[ 77] = ZI.1.EV_PK_S2 INCLUDE=@airport_taz@
         MW[ 78] = ZI.1.EV_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[ 79] = ZI.1.EV_RN_DA INCLUDE=@airport_taz@
         MW[ 80] = ZI.1.EV_RN_S2 INCLUDE=@airport_taz@
         MW[ 81] = ZI.1.EV_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[ 82] = ZI.1.EV_TX_DA INCLUDE=@airport_taz@
         MW[ 83] = ZI.1.EV_TX_S2 INCLUDE=@airport_taz@
         MW[ 84] = ZI.1.EV_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[ 85] = ZI.1.EV_LI_DA INCLUDE=@airport_taz@
         MW[ 86] = ZI.1.EV_LI_S2 INCLUDE=@airport_taz@
         MW[ 87] = ZI.1.EV_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[ 88] = ZI.1.EV_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[ 89] = ZI.1.EV_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[ 90] = ZI.1.EV_CH_S3 INCLUDE=@airport_taz@

         ; ==================== EA ====================
         ; Escort
         MW[101] = ZI.2.EA_ES_DA INCLUDE=@airport_taz@
         MW[102] = ZI.2.EA_ES_S2 INCLUDE=@airport_taz@
         MW[103] = ZI.2.EA_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[104] = ZI.2.EA_PK_DA INCLUDE=@airport_taz@
         MW[105] = ZI.2.EA_PK_S2 INCLUDE=@airport_taz@
         MW[106] = ZI.2.EA_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[107] = ZI.2.EA_RN_DA INCLUDE=@airport_taz@
         MW[108] = ZI.2.EA_RN_S2 INCLUDE=@airport_taz@
         MW[109] = ZI.2.EA_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[110] = ZI.2.EA_TX_DA INCLUDE=@airport_taz@
         MW[111] = ZI.2.EA_TX_S2 INCLUDE=@airport_taz@
         MW[112] = ZI.2.EA_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[113] = ZI.2.EA_LI_DA INCLUDE=@airport_taz@
         MW[114] = ZI.2.EA_LI_S2 INCLUDE=@airport_taz@
         MW[115] = ZI.2.EA_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[116] = ZI.2.EA_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[117] = ZI.2.EA_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[118] = ZI.2.EA_CH_S3 INCLUDE=@airport_taz@

         ; ==================== AM ====================
         ; Escort
         MW[119] = ZI.2.AM_ES_DA INCLUDE=@airport_taz@
         MW[120] = ZI.2.AM_ES_S2 INCLUDE=@airport_taz@
         MW[121] = ZI.2.AM_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[122] = ZI.2.AM_PK_DA INCLUDE=@airport_taz@
         MW[123] = ZI.2.AM_PK_S2 INCLUDE=@airport_taz@
         MW[124] = ZI.2.AM_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[125] = ZI.2.AM_RN_DA INCLUDE=@airport_taz@
         MW[126] = ZI.2.AM_RN_S2 INCLUDE=@airport_taz@
         MW[127] = ZI.2.AM_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[128] = ZI.2.AM_TX_DA INCLUDE=@airport_taz@
         MW[129] = ZI.2.AM_TX_S2 INCLUDE=@airport_taz@
         MW[130] = ZI.2.AM_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[131] = ZI.2.AM_LI_DA INCLUDE=@airport_taz@
         MW[132] = ZI.2.AM_LI_S2 INCLUDE=@airport_taz@
         MW[133] = ZI.2.AM_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[134] = ZI.2.AM_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[135] = ZI.2.AM_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[136] = ZI.2.AM_CH_S3 INCLUDE=@airport_taz@

         ; ==================== MD ====================
         ; Escort
         MW[137] = ZI.2.MD_ES_DA INCLUDE=@airport_taz@
         MW[138] = ZI.2.MD_ES_S2 INCLUDE=@airport_taz@
         MW[139] = ZI.2.MD_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[140] = ZI.2.MD_PK_DA INCLUDE=@airport_taz@
         MW[141] = ZI.2.MD_PK_S2 INCLUDE=@airport_taz@
         MW[142] = ZI.2.MD_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[143] = ZI.2.MD_RN_DA INCLUDE=@airport_taz@
         MW[144] = ZI.2.MD_RN_S2 INCLUDE=@airport_taz@
         MW[145] = ZI.2.MD_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[146] = ZI.2.MD_TX_DA INCLUDE=@airport_taz@
         MW[147] = ZI.2.MD_TX_S2 INCLUDE=@airport_taz@
         MW[148] = ZI.2.MD_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[149] = ZI.2.MD_LI_DA INCLUDE=@airport_taz@
         MW[150] = ZI.2.MD_LI_S2 INCLUDE=@airport_taz@
         MW[151] = ZI.2.MD_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[152] = ZI.2.MD_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[153] = ZI.2.MD_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[154] = ZI.2.MD_CH_S3 INCLUDE=@airport_taz@

         ; ==================== PM ====================
         ; Escort
         MW[155] = ZI.2.PM_ES_DA INCLUDE=@airport_taz@
         MW[156] = ZI.2.PM_ES_S2 INCLUDE=@airport_taz@
         MW[157] = ZI.2.PM_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[158] = ZI.2.PM_PK_DA INCLUDE=@airport_taz@
         MW[159] = ZI.2.PM_PK_S2 INCLUDE=@airport_taz@
         MW[160] = ZI.2.PM_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[161] = ZI.2.PM_RN_DA INCLUDE=@airport_taz@
         MW[162] = ZI.2.PM_RN_S2 INCLUDE=@airport_taz@
         MW[163] = ZI.2.PM_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[164] = ZI.2.PM_TX_DA INCLUDE=@airport_taz@
         MW[165] = ZI.2.PM_TX_S2 INCLUDE=@airport_taz@
         MW[166] = ZI.2.PM_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[167] = ZI.2.PM_LI_DA INCLUDE=@airport_taz@
         MW[168] = ZI.2.PM_LI_S2 INCLUDE=@airport_taz@
         MW[169] = ZI.2.PM_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[170] = ZI.2.PM_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[171] = ZI.2.PM_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[172] = ZI.2.PM_CH_S3 INCLUDE=@airport_taz@

         ; ==================== EV ====================
         ; Escort
         MW[173] = ZI.2.EV_ES_DA INCLUDE=@airport_taz@
         MW[174] = ZI.2.EV_ES_S2 INCLUDE=@airport_taz@
         MW[175] = ZI.2.EV_ES_S3 INCLUDE=@airport_taz@
         ; Park
         MW[176] = ZI.2.EV_PK_DA INCLUDE=@airport_taz@
         MW[177] = ZI.2.EV_PK_S2 INCLUDE=@airport_taz@
         MW[178] = ZI.2.EV_PK_S3 INCLUDE=@airport_taz@
         ; Rent
         MW[179] = ZI.2.EV_RN_DA INCLUDE=@airport_taz@
         MW[180] = ZI.2.EV_RN_S2 INCLUDE=@airport_taz@
         MW[181] = ZI.2.EV_RN_S3 INCLUDE=@airport_taz@
         ; Taxi
         MW[182] = ZI.2.EV_TX_DA INCLUDE=@airport_taz@
         MW[183] = ZI.2.EV_TX_S2 INCLUDE=@airport_taz@
         MW[184] = ZI.2.EV_TX_S3 INCLUDE=@airport_taz@
         ; Limo
         MW[185] = ZI.2.EV_LI_DA INCLUDE=@airport_taz@
         MW[186] = ZI.2.EV_LI_S2 INCLUDE=@airport_taz@
         MW[187] = ZI.2.EV_LI_S3 INCLUDE=@airport_taz@
         ; Van
         MW[188] = ZI.2.EV_VN_S3 INCLUDE=@airport_taz@
         ; Hotel
         MW[189] = ZI.2.EV_HT_S3 INCLUDE=@airport_taz@
         ; Charter
         MW[190] = ZI.2.EV_CH_S3 INCLUDE=@airport_taz@

      endrun

   endloop ; airport

   ; ----------------------------------------------------------------------------------------------------------------
   ; STEP 3  
   ; Finally, a highway-assignment ready matrix file is created for each time-of-day interval.
   ; ----------------------------------------------------------------------------------------------------------------
   loop timeperiod = 1,5
      if (timeperiod = 1) token_tp = 'EA'
      if (timeperiod = 2) token_tp = 'AM'
      if (timeperiod = 3) token_tp = 'MD'
      if (timeperiod = 4) token_tp = 'MD'
      if (timeperiod = 5) token_tp = 'PM'

      run pgm = matrix
   
         filei mati[1] = @token_year@_toSFO.tpp
         filei mati[2] = @token_year@_fromSFO_t.tpp
      
         filei mati[3] = @token_year@_toOAK.tpp
         filei mati[4] = @token_year@_fromOAK_t.tpp
      
         filei mati[5] = @token_year@_toSJC.tpp
         filei mati[6] = @token_year@_fromSJC_t.tpp
      
         fileo mato[1] = @token_year@_tripsAirPax@token_tp@.tpp, mo =  1- 6, name =  DA, SR2, SR3, DATOLL, SR2TOLL, SR3TOLL
         
        ; combine trips by occupancy ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        ;   - drive alone free
        mw[1] = mi.1.@token_tp@_Escort_DA   + mi.1.@token_tp@_Park_DA   + mi.1.@token_tp@_Rent_DA   + mi.1.@token_tp@_Taxi_DA   + mi.1.@token_tp@_Limo_DA   +
                mi.2.@token_tp@_Escort_DA.T + mi.2.@token_tp@_Park_DA.T + mi.2.@token_tp@_Rent_DA.T + mi.2.@token_tp@_Taxi_DA.T + mi.2.@token_tp@_Limo_DA.T +
                mi.3.@token_tp@_Escort_DA   + mi.3.@token_tp@_Park_DA   + mi.3.@token_tp@_Rent_DA   + mi.3.@token_tp@_Taxi_DA   + mi.3.@token_tp@_Limo_DA   + 
                mi.4.@token_tp@_Escort_DA.T + mi.4.@token_tp@_Park_DA.T + mi.4.@token_tp@_Rent_DA.T + mi.4.@token_tp@_Taxi_DA.T + mi.4.@token_tp@_Limo_DA.T +
                mi.5.@token_tp@_Escort_DA   + mi.5.@token_tp@_Park_DA   + mi.5.@token_tp@_Rent_DA   + mi.5.@token_tp@_Taxi_DA   + mi.5.@token_tp@_Limo_DA   +
                mi.6.@token_tp@_Escort_DA.T + mi.6.@token_tp@_Park_DA.T + mi.6.@token_tp@_Rent_DA.T + mi.6.@token_tp@_Taxi_DA.T + mi.6.@token_tp@_Limo_DA.T
              
        ;   - shared ride 2 free
        mw[2] = mi.1.@token_tp@_Escort_S2   + mi.1.@token_tp@_Park_S2   + mi.1.@token_tp@_Rent_S2   + mi.1.@token_tp@_Taxi_S2   + mi.1.@token_tp@_Limo_S2   +
                mi.2.@token_tp@_Escort_S2.T + mi.2.@token_tp@_Park_S2.T + mi.2.@token_tp@_Rent_S2.T + mi.2.@token_tp@_Taxi_S2.T + mi.2.@token_tp@_Limo_S2.T +
                mi.3.@token_tp@_Escort_S2   + mi.3.@token_tp@_Park_S2   + mi.3.@token_tp@_Rent_S2   + mi.3.@token_tp@_Taxi_S2   + mi.3.@token_tp@_Limo_S2   +
                mi.4.@token_tp@_Escort_S2.T + mi.4.@token_tp@_Park_S2.T + mi.4.@token_tp@_Rent_S2.T + mi.4.@token_tp@_Taxi_S2.T + mi.4.@token_tp@_Limo_S2.T +
                mi.5.@token_tp@_Escort_S2   + mi.5.@token_tp@_Park_S2   + mi.5.@token_tp@_Rent_S2   + mi.5.@token_tp@_Taxi_S2   + mi.5.@token_tp@_Limo_S2   +
                mi.6.@token_tp@_Escort_S2.T + mi.6.@token_tp@_Park_S2.T + mi.6.@token_tp@_Rent_S2.T + mi.6.@token_tp@_Taxi_S2.T + mi.6.@token_tp@_Limo_S2.T
              
         ;   - shared ride 3 free
         mw[3] = mi.1.@token_tp@_Escort_S3   + mi.1.@token_tp@_Park_S3   + mi.1.@token_tp@_Rent_S3   + mi.1.@token_tp@_Taxi_S3   + mi.1.@token_tp@_Limo_S3   + mi.1.@token_tp@_Van_S3   + mi.1.@token_tp@_Hotel_S3   + mi.1.@token_tp@_Chart_S3   +
                 mi.2.@token_tp@_Escort_S3.T + mi.2.@token_tp@_Park_S3.T + mi.2.@token_tp@_Rent_S3.T + mi.2.@token_tp@_Taxi_S3.T + mi.2.@token_tp@_Limo_S3.T + mi.2.@token_tp@_Van_S3.T + mi.2.@token_tp@_Hotel_S3.T + mi.2.@token_tp@_Chart_S3.T +
                 mi.3.@token_tp@_Escort_S3   + mi.3.@token_tp@_Park_S3   + mi.3.@token_tp@_Rent_S3   + mi.3.@token_tp@_Taxi_S3   + mi.3.@token_tp@_Limo_S3   + mi.3.@token_tp@_Van_S3   + mi.3.@token_tp@_Hotel_S3   + mi.3.@token_tp@_Chart_S3   +
                 mi.4.@token_tp@_Escort_S3.T + mi.4.@token_tp@_Park_S3.T + mi.4.@token_tp@_Rent_S3.T + mi.4.@token_tp@_Taxi_S3.T + mi.4.@token_tp@_Limo_S3.T + mi.4.@token_tp@_Van_S3.T + mi.4.@token_tp@_Hotel_S3.T + mi.4.@token_tp@_Chart_S3.T +
                 mi.5.@token_tp@_Escort_S3   + mi.5.@token_tp@_Park_S3   + mi.5.@token_tp@_Rent_S3   + mi.5.@token_tp@_Taxi_S3   + mi.5.@token_tp@_Limo_S3   + mi.5.@token_tp@_Van_S3   + mi.5.@token_tp@_Hotel_S3   + mi.5.@token_tp@_Chart_S3   +
                 mi.6.@token_tp@_Escort_S3.T + mi.6.@token_tp@_Park_S3.T + mi.6.@token_tp@_Rent_S3.T + mi.6.@token_tp@_Taxi_S3.T + mi.6.@token_tp@_Limo_S3.T + mi.6.@token_tp@_Van_S3.T + mi.6.@token_tp@_Hotel_S3.T + mi.6.@token_tp@_Chart_S3.T
   
        ;   - drive alone toll (assume no trips pay HOT fee)
        mw[4] = 0
      
        ;   - shared ride 2 toll (assume no trips pay HOT fee)
        mw[5] = 0
      
        ;   - shared ride 3 toll (assume no trips pay HOT fee)
        mw[6] = 0
      
      endrun
   endloop ; timeperiod

endloop ; fYear