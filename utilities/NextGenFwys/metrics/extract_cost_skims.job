; ----------------------------------------------------------------------------------------------------------------
; extract_cost_skims.job
;
; This script extracts detailed household auto and transit costs from the Cube skim
; matrices into csv files for commute_tours_by_inc_tp.r.
;
; Input: 
;   (1) skims\HWYSKM[EA,AM,MD,PM,EV].tpp: roadways skims for that all time periods
;   (2) skims\trnskm[EA,AM,MD,PM,EV]_[wlk|drv]_[com,hvy,exp,lrf,loc]_[wlk|drv].tpp: 
;       transit skims for that all time period
;
; Output:
;   (1) skims\HWYSKM_cost_[EA,AM,MD,PM,EV].csv - including distances, bridge tolls 
;       and value tolls for all auto modes.  Units are unchanged from skims:
;       Bridge toll and value tolls are in year 2000 cents.
;       Distances are in miles.
;   (2) skims\trnskm_cost_[EA,AM,MD,PM,EV].csv - including fare and drive dist 
;       (if applicable) for all transit modes. Units are unchanged from skims:
;       Fares are in year 2000 cents; drive distance is in (miles x 100)
;
; See also: (1) HwySkims.job
;           (2) TransitSkims.job
;           (3) commute_tours_by_inc_tp.r
; ----------------------------------------------------------------------------------------------------------------

; Loop through time periods
loop time_period_num = 1,5
  if (time_period_num = 1)
    token_time_period = 'EA'
  elseif (time_period_num = 2)
    token_time_period = 'AM'
  elseif (time_period_num = 3)
    token_time_period = 'MD'
  elseif (time_period_num = 4)
    token_time_period = 'PM'
  elseif (time_period_num = 5)
    token_time_period = 'EV'
  endif

  DistributeMultistep processid = 'ctramp', processNum = time_period_num, commpath = '%COMMPATH%'

  ; Extract hwy skim costs tables into a text file of condensed form
  run PGM=MATRIX
    filei mati[1] = "skims\HWYSKM@token_time_period@.tpp"
    fileo mato[1] = "skims\HWYSKM_cost_@token_time_period@_noheader.csv", FORMAT=TXT, DELIMITER=",", PATTERN=IJM:V, MO=1,3,4,6,7,8,10,11,13,14,15,17,18,20,21,22

    MW[1] = 1
    FILLMW MW[2] = MI.1.1(21)
    ; 2: TIMEDA       5: TOLLTIMEDA     9: TIMES2      12: TOLLTIMES2    16: TIMES3      19: TOLLTIMES3 
    ; 3: DISTDA       6: TOLLDISTDA    10: DISTS2      13: TOLLDISTS2    17: DISTS3      20: TOLLDISTS3 
    ; 4: BTOLLDA      7: TOLLBTOLLDA   11: BTOLLS2     14: TOLLBTOLLS2   18: BTOLLS3     21: TOLLBTOLLS3
    ;                 8: TOLLVTOLLDA                   15: TOLLVTOLLS2                   22: TOLLVTOLLS3
  endrun

  ; add header
  *echo orig, dest, one_a, one_b, DISTDA, BTOLLDA, TOLLDISTDA, TOLLBTOLLDA, TOLLVTOLLDA, DISTS2, BTOLLS2, TOLLDISTS2, TOLLBTOLLS2, TOLLVTOLLS2, DISTS3, BTOLLS3, TOLLDISTS3, TOLLBTOLLS3, TOLLVTOLLS3 >   "skims\HWYSKM_cost_@token_time_period@.csv"
  *type "skims\HWYSKM_cost_@token_time_period@_noheader.csv" >> "skims\HWYSKM_cost_@token_time_period@.csv"
  *del "skims\HWYSKM_cost_@token_time_period@_noheader.csv"

  ; Extract transit skim costs tables into a text file of condensed form
  run PGM=MATRIX
    filei mati[1]  = "skims\trnskm@token_time_period@_wlk_com_wlk.tpp"
    filei mati[2]  = "skims\trnskm@token_time_period@_wlk_hvy_wlk.tpp"
    filei mati[3]  = "skims\trnskm@token_time_period@_wlk_exp_wlk.tpp"
    filei mati[4]  = "skims\trnskm@token_time_period@_wlk_lrf_wlk.tpp"
    filei mati[5]  = "skims\trnskm@token_time_period@_wlk_loc_wlk.tpp"

    filei mati[6]  = "skims\trnskm@token_time_period@_drv_com_wlk.tpp"
    filei mati[7]  = "skims\trnskm@token_time_period@_drv_hvy_wlk.tpp"
    filei mati[8]  = "skims\trnskm@token_time_period@_drv_exp_wlk.tpp"
    filei mati[9]  = "skims\trnskm@token_time_period@_drv_lrf_wlk.tpp"
    filei mati[10] = "skims\trnskm@token_time_period@_drv_loc_wlk.tpp"

    filei mati[11] = "skims\trnskm@token_time_period@_wlk_com_drv.tpp"
    filei mati[12] = "skims\trnskm@token_time_period@_wlk_hvy_drv.tpp"
    filei mati[13] = "skims\trnskm@token_time_period@_wlk_exp_drv.tpp"
    filei mati[14] = "skims\trnskm@token_time_period@_wlk_lrf_drv.tpp"
    filei mati[15] = "skims\trnskm@token_time_period@_wlk_loc_drv.tpp"

    fileo mato[1] = "skims\trnskm_cost_@token_time_period@_noheader.csv", FORMAT=TXT, DELIMITER=",", PATTERN=IJM:V, MO=1-26
    mw[1] = 1

    MW[2] = MI.1.fare
    MW[3] = MI.2.fare
    MW[4] = MI.3.fare
    MW[5] = MI.4.fare
    MW[6] = MI.5.fare

    MW[7]  = MI.6.fare
    MW[8]  = MI.6.ddist
    MW[9]  = MI.7.fare
    MW[10] = MI.7.ddist
    MW[11] = MI.8.fare
    MW[12] = MI.8.ddist
    MW[13] = MI.9.fare
    MW[14] = MI.9.ddist
    MW[15] = MI.10.fare
    MW[16] = MI.10.ddist

    MW[17] = MI.11.fare
    MW[18] = MI.11.ddist
    MW[19] = MI.12.fare
    MW[20] = MI.12.ddist
    MW[21] = MI.13.fare
    MW[22] = MI.13.ddist
    MW[23] = MI.14.fare
    MW[24] = MI.14.ddist
    MW[25] = MI.15.fare
    MW[26] = MI.15.ddist 
  endrun

  ; add header
  *echo orig, dest, one_a, one_b, fare_wlk_com_wlk, fare_wlk_hvy_wlk, fare_wlk_exp_wlk, fare_wlk_lrf_wlk, fare_wlk_loc_wlk, fare_drv_com_wlk, ddist_drv_com_wlk, fare_drv_hvy_wlk, ddist_drv_hvy_wlk, fare_drv_exp_wlk, ddist_drv_exp_wlk, fare_drv_lrf_wlk, ddist_drv_lrf_wlk, fare_drv_loc_wlk, ddist_drv_loc_wlk, fare_wlk_com_drv, ddist_wlk_com_drv, fare_wlk_hvy_drv, ddist_wlk_hvy_drv, fare_wlk_exp_drv, ddist_wlk_exp_drv, fare_wlk_lrf_drv, ddist_wlk_lrf_drv, fare_wlk_loc_drv, ddist_wlk_loc_drv >   "skims\trnskm_cost_@token_time_period@.csv"
  *type "skims\trnskm_cost_@token_time_period@_noheader.csv" >> "skims\trnskm_cost_@token_time_period@.csv"
  *del "skims\trnskm_cost_@token_time_period@_noheader.csv"

  EndDistributeMultistep

endloop
Wait4Files files=CTRAMP1.script.end, CTRAMP2.script.end, CTRAMP3.script.end, CTRAMP4.script.end, CTRAMP5.script.end,
           printfiles = merge, deldistribfiles = t, CheckReturnCode = t
