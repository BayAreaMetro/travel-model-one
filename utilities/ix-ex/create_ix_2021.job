; ----------------------------------------------------------------------------------------------------------------
;
; create_ix_2021.job
;
; Working directory is M:\Development\Travel Model One\InternalExternal\RTP2025_PBA50+
;
; ----------------------------------------------------------------------------------------------------------------
FIRST_EXT_ZONE = 1455

;=========  Step 1: Create 2021 using targets
run pgm=matrix

   ; Begin with 2005 base year matrix established above
   mati = "M:\Development\Travel Model One\InternalExternal\RTP2021_PBA50\ixDaily2015.tpp"
   
   ; Write out a year-specific matrix
   mato = ixDaily2021.tpp, mo = 1-4, name = ix_daily_da, ix_daily_sr2, ix_daily_sr3, ix_daily_total

   ; Write out totals
   reco = ixDaily2021_totals.dbf, fields=EXT_ZONE(5.0), PROD(12.4), ATTR(12.4)

   ; Also read the totals from 2015
   filei lookupi[1] = "M:\Development\Travel Model One\InternalExternal\RTP2021_PBA50\ixDaily2015_totals.dbf"
   filei lookupi[2] = "E:\GitHub\travel-model-one\utilities\ix-ex\totals_baseyears.dbf"
  
   ; existing
   lookup lookupi=1, name=totals_2015,
     lookup[1]=EXT_ZONE, result=PROD,  ; productions
     lookup[2]=EXT_ZONE, result=ATTR   ; attractions

   ; target
   lookup lookupi=2, name=totals_2021,
     lookup[1]=EXT_ZONE, result=TOTAL_2021

   ; summarize by external production and attraction zones - daily total
   ARRAY factor=21, prod_tot=21, attr_tot=21

   IF (I==1)
     ; first, figure out desired daily total
     LOOP EXT_ZONE = @FIRST_EXT_ZONE@,1475
       prod_total_2015 = totals_2015(1,EXT_ZONE)
       attr_total_2015 = totals_2015(2,EXT_ZONE)

       ; figure out 2015 desired total -- grow the non-commute share by the slope
       total_2015 = prod_total_2015 + attr_total_2015
       total_2021 = totals_2021(1,EXT_ZONE)

       if (total_2015 == 0)
         factor[EXT_ZONE-@FIRST_EXT_ZONE@] = 1.0
       else
         factor[EXT_ZONE-@FIRST_EXT_ZONE@] = total_2021/total_2015
       endif

       PRINT LIST="2005: For external zone ",EXT_ZONE, " prod_2005: ", prod_total_2015, " attr_2005: ", attr_total_2015, 
         " total_2015: ", total_2015, " total_2021: ", total_2021, " => factor: ",factor[EXT_ZONE-@FIRST_EXT_ZONE@]

     ENDLOOP
     ; however there are X/X trips between 1461 and 1462
     TOT_1461_1462 = MATVAL(1,1,1461,1462) + MATVAL(1,2,1461,1462) + MATVAL(1,3,1461,1462)
     TOT_1462_1461 = MATVAL(1,1,1462,1461) + MATVAL(1,2,1462,1461) + MATVAL(1,3,1462,1461)

     ; so if we scale those according to 1461, then 1462 will be short this much
     TOT_1461 = (TOT_1461_1462 + TOT_1462_1461) * factor[1461-@FIRST_EXT_ZONE@]
     TOT_1462 = (TOT_1461_1462 + TOT_1462_1461) * factor[1462-@FIRST_EXT_ZONE@]
     DIFF_1462 = TOT_1462 - TOT_1461

     PRINT LIST="XX trips for 1461-1462: ", TOT_1461_1462, " 1462-1461: ", TOT_1462_1461
     PRINT LIST="Using 1461 factor: ", TOT_1461, "; Using 1462 factor: ", TOT_1462, "; 1462 will be short by: ", DIFF_1462

     ; so make up for it by upping the 2015 target. Assuming two XX vals factor to TOT_1461, remove them from the calc
     total_2015 = totals_2015(1,1462) + totals_2015(2,1462)
     total_2021 = totals_2021(1,1462)

     factor[1462 - @FIRST_EXT_ZONE@] = (total_2021-TOT_1461)/(total_2015-TOT_1461_1462-TOT_1462_1461)
     PRINT LIST=" => Updating factor 1462 to ", factor[1462 - @FIRST_EXT_ZONE@] 

   ENDIF

   jloop
   
      ; Apply gateway-specific adjustment factors 
      ; (no X/X trips mean the gateways act independently, other than the pair we compensated for above)
      if (I >= @FIRST_EXT_ZONE@)
         ; 1461/1462 have XX trips -- use 1461 factor
         if ((I=1462) & (J=1461))
           mw[1] = mi.1.IX_Daily_DA  * factor[J-@FIRST_EXT_ZONE@]
           mw[2] = mi.1.IX_Daily_SR2 * factor[J-@FIRST_EXT_ZONE@]
           mw[3] = mi.1.IX_Daily_SR3 * factor[J-@FIRST_EXT_ZONE@]
         else
           mw[1] = mi.1.IX_Daily_DA  * factor[I-@FIRST_EXT_ZONE@]
           mw[2] = mi.1.IX_Daily_SR2 * factor[I-@FIRST_EXT_ZONE@]
           mw[3] = mi.1.IX_Daily_SR3 * factor[I-@FIRST_EXT_ZONE@]
         endif
      elseif (J >= @FIRST_EXT_ZONE@)
         mw[1] = mi.1.IX_Daily_DA  * factor[J-@FIRST_EXT_ZONE@]
         mw[2] = mi.1.IX_Daily_SR2 * factor[J-@FIRST_EXT_ZONE@]
         mw[3] = mi.1.IX_Daily_SR3 * factor[J-@FIRST_EXT_ZONE@]
      endif

      ; Sum the working matrices to form the total
      mw[4] = mw[1] + mw[2] + mw[3]
      IF (J>1454) attr_tot[J-@FIRST_EXT_ZONE@] = attr_tot[J-@FIRST_EXT_ZONE@] + Mw[4]

   endjloop
   
   IF (I>1454) prod_tot[I-@FIRST_EXT_ZONE@] = prod_tot[I-@FIRST_EXT_ZONE@] + ROWSUM(4)

   ; write production/attraction totals to the log file and dbf
   IF (I==ZONES)
     print LIST="-----RESULTS-----"
     LOOP EXT_ZONE = @FIRST_EXT_ZONE@,1475
       total = prod_tot[EXT_ZONE-@FIRST_EXT_ZONE@] + attr_tot[EXT_ZONE-@FIRST_EXT_ZONE@]
       print LIST="For external zone ",EXT_ZONE, " production: ", prod_tot[EXT_ZONE-@FIRST_EXT_ZONE@], " attraction: ", attr_tot[EXT_ZONE-@FIRST_EXT_ZONE@], " total: ", total
       
       RO.EXT_ZONE = EXT_ZONE
       RO.PROD     = prod_tot[EXT_ZONE-@FIRST_EXT_ZONE@]
       RO.ATTR     = attr_tot[EXT_ZONE-@FIRST_EXT_ZONE@]
       write RECO=1
     ENDLOOP
   ENDIF
   
endrun

