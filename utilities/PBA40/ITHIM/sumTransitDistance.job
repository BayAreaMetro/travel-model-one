; Transit trips and times by access / mode / egress
; 1. Transit in-vehicle distance by mode
; 2. Drive distance
; 3. Walk distance
;
;  Uses Transit trips * Transit skims
;  Based on ..\metrics\sumTransitTime.job

*del metrics\ITHIM\DistanceTraveledByFacilityType_transit.csv

; Loop thru transit modes
loop path = 1,5

  if (path = 1) token_path = 'com'  ; commuter rail or long-haul premium
  if (path = 2) token_path = 'hvy'  ; heavy rail or medium-haul premium
  if (path = 3) token_path = 'exp'  ; express bus or medium-haul basic
  if (path = 4) token_path = 'lrf'  ; light rail (or ferry) or short-haul premium
  if (path = 5) token_path = 'loc'  ; local bus or short-haul basic

  DistributeMultistep processid = 'ctramp', processNum = path, commpath = 'm:\commpath'

    ; Loop thru access + egress modes
    loop accegg = 1,3
      if (accegg = 1)
        token_access = 'wlk'
        token_egress = 'wlk'
      elseif (accegg = 2)
        token_access = 'drv'
        token_egress = 'wlk'
      elseif (accegg = 3)
        token_access = 'wlk'
        token_egress = 'drv'
      endif

    RUN PGM = MATRIX
      ; Read person trips 
      ;  1. da, 2. datoll, 3. sr2, 4. sr2toll, 5. sr3, 6. sr3toll, 7. walk, 8. bike,
      ;  9. wlk_loc_wlk, wlk_lrf_wlk, wlk_exp_wlk, wlk_hvy_wlk, wlk_com_wlk,
      ; 14. drv_loc_wlk, drv_lrf_wlk, drv_exp_wlk, drv_hvy_wlk, drv_com_wlk,
      ; 19. wlk_loc_drv, wlk_lrf_drv, wlk_exp_drv, wlk_hvy_drv, wlk_com_drv
      FILEI MATI[1] = "main\tripsEAallinc.tpp"
      FILEI MATI[2] = "main\tripsAMallinc.tpp"
      FILEI MATI[3] = "main\tripsMDallinc.tpp"
      FILEI MATI[4] = "main\tripsPMallinc.tpp"
      FILEI MATI[5] = "main\tripsEVallinc.tpp"

      ; Read transit skims
      FILEI MATI[6] = "skims\trnskmEA_@token_access@_@token_path@_@token_egress@_temp.tpp"
      FILEI MATI[7] = "skims\trnskmAM_@token_access@_@token_path@_@token_egress@_temp.tpp"
      FILEI MATI[8] = "skims\trnskmMD_@token_access@_@token_path@_@token_egress@_temp.tpp"
      FILEI MATI[9] = "skims\trnskmPM_@token_access@_@token_path@_@token_egress@_temp.tpp"
      FILEI MATI[10]= "skims\trnskmEV_@token_access@_@token_path@_@token_egress@_temp.tpp"

      FILEO PRINTO[1] = "metrics\ITHIM\DistanceTraveledByFacilityType_transit_@token_path@.csv", append=T

      ; EA Transit Trips
      MW[1]   = mi.1.@token_access@_@token_path@_@token_egress@ ; Trips
      MW[2]   = MW[1] * mi.6.distLoc                            ; Trips x distance local bus
      MW[3]   = MW[1] * mi.6.distLRF                            ; Trips x distance light rail / ferry
      MW[4]   = MW[1] * mi.6.distEXP                            ; Trips x distance express bus
      MW[5]   = MW[1] * mi.6.distHVY                            ; Trips x distance heavy rail
      MW[6]   = MW[1] * mi.6.distCOM                            ; Trips x distance commuter rail
      MW[7]   = MW[1] * mi.6.distFerry                          ; Trips x distance ferry
      MW[8]   = MW[1] * mi.6.ddist                              ; Trips x distance drive
      MW[9]   = MW[1] * (mi.6.wacc+mi.6.waux+mi.6.wegr)         ; Trips x time walk

      ; AM Transit Trips
      MW[11]  = mi.2.@token_access@_@token_path@_@token_egress@ ; Trips
      MW[12]  = MW[11] * mi.7.distLoc                            ; Trips x distance local bus
      MW[13]  = MW[11] * mi.7.distLRF                            ; Trips x distance light rail / ferry
      MW[14]  = MW[11] * mi.7.distEXP                            ; Trips x distance express bus
      MW[15]  = MW[11] * mi.7.distHVY                            ; Trips x distance heavy rail
      MW[16]  = MW[11] * mi.7.distCOM                            ; Trips x distance commuter rail
      MW[17]  = MW[11] * mi.7.distFerry                          ; Trips x distance ferry
      MW[18]  = MW[11] * mi.7.ddist                              ; Trips x distance drive
      MW[19]  = MW[11] * (mi.7.wacc+mi.7.waux+mi.7.wegr)         ; Trips x time walk

      ; MD Transit Trips
      MW[21]  = mi.3.@token_access@_@token_path@_@token_egress@ ; Trips
      MW[22]  = MW[21] * mi.8.distLoc                            ; Trips x distance local bus
      MW[23]  = MW[21] * mi.8.distLRF                            ; Trips x distance light rail / ferry
      MW[24]  = MW[21] * mi.8.distEXP                            ; Trips x distance express bus
      MW[25]  = MW[21] * mi.8.distHVY                            ; Trips x distance heavy rail
      MW[26]  = MW[21] * mi.8.distCOM                            ; Trips x distance commuter rail
      MW[27]  = MW[21] * mi.8.distFerry                          ; Trips x distance ferry
      MW[28]  = MW[21] * mi.8.ddist                              ; Trips x distance drive
      MW[29]  = MW[21] * (mi.8.wacc+mi.8.waux+mi.8.wegr)         ; Trips x time walk

      ; PM Transit Trips
      MW[31]  = mi.4.@token_access@_@token_path@_@token_egress@ ; Trips
      MW[32]  = MW[31] * mi.9.distLoc                            ; Trips x distance local bus
      MW[33]  = MW[31] * mi.9.distLRF                            ; Trips x distance light rail / ferry
      MW[34]  = MW[31] * mi.9.distEXP                            ; Trips x distance express bus
      MW[35]  = MW[31] * mi.9.distHVY                            ; Trips x distance heavy rail
      MW[36]  = MW[31] * mi.9.distCOM                            ; Trips x distance commuter rail
      MW[37]  = MW[31] * mi.9.distFerry                          ; Trips x distance ferry
      MW[38]  = MW[31] * mi.9.ddist                              ; Trips x distance drive
      MW[39]  = MW[31] * (mi.9.wacc+mi.9.waux+mi.9.wegr)         ; Trips x time walk

      ; EV Transit Trips
      MW[41]  = mi.5.@token_access@_@token_path@_@token_egress@ ; Trips
      MW[42]  = MW[41] * mi.10.distLoc                            ; Trips x distance local bus
      MW[43]  = MW[41] * mi.10.distLRF                            ; Trips x distance light rail / ferry
      MW[44]  = MW[41] * mi.10.distEXP                            ; Trips x distance express bus
      MW[45]  = MW[41] * mi.10.distHVY                            ; Trips x distance heavy rail
      MW[46]  = MW[41] * mi.10.distCOM                            ; Trips x distance commuter rail
      MW[47]  = MW[41] * mi.10.distFerry                          ; Trips x distance ferry
      MW[48]  = MW[41] * mi.10.ddist                              ; Trips x distance drive
      MW[49]  = MW[41] * (mi.10.wacc+mi.10.waux+mi.10.wegr)       ; Trips x time walk

      Trips        = Trips       + ROWSUM(1) + ROWSUM(11) + ROWSUM(21) + ROWSUM(31) + ROWSUM(41)
      TripDistLoc  = TripDistLoc + ROWSUM(2) + ROWSUM(12) + ROWSUM(22) + ROWSUM(32) + ROWSUM(42)
      TripDistLRF  = TripDistLRF + ROWSUM(3) + ROWSUM(13) + ROWSUM(23) + ROWSUM(33) + ROWSUM(43)
      TripDistExp  = TripDistExp + ROWSUM(4) + ROWSUM(14) + ROWSUM(24) + ROWSUM(34) + ROWSUM(44)
      TripDistHvy  = TripDistHvy + ROWSUM(5) + ROWSUM(15) + ROWSUM(25) + ROWSUM(35) + ROWSUM(45)
      TripDistCom  = TripDistCom + ROWSUM(6) + ROWSUM(16) + ROWSUM(26) + ROWSUM(36) + ROWSUM(46)
      TripDistFer  = TripDistFer + ROWSUM(7) + ROWSUM(17) + ROWSUM(27) + ROWSUM(37) + ROWSUM(47)
      TripDistDrv  = TripDistDrv + ROWSUM(8) + ROWSUM(18) + ROWSUM(28) + ROWSUM(38) + ROWSUM(48)
      TripTimeWlk  = TripTimeWlk + ROWSUM(9) + ROWSUM(19) + ROWSUM(29) + ROWSUM(39) + ROWSUM(49)

      Name = '@token_access@_@token_path@_@token_egress@'
      if(I = 1 & @path@ = 1 & @accegg@ = 1)
        PRINT PRINTO=1 CSV=T LIST= "Access","Mode","Egress",
          "Transit Trips",
          "Local Bus Person Miles",
          "Light Rail & Ferry Person Miles",
          "Express Bus Person Miles",
          "Heavy Rail Person Miles",
          "Commuter Rail Person Miles",
          "Ferry Person Miles",
          "Drive Access/Egress Person Miles",
          "Walk Access/Egress Person Miles"
      endif
      if(I=1475)
        TripDistLoc_miles = TripDistLoc / 100
        TripDistLRF_miles = TripDistLRF / 100
        TripDistExp_miles = TripDistExp / 100
        TripDistHvy_miles = TripDistHvy / 100
        TripDistCom_miles = TripDistCom / 100
        TripDistFer_miles = TripDistFer / 100
        TripDistDrv_miles = TripDistDrv / 100

        TripTimeWlk_hours = TripTimeWlk / 6000
        TripDistWlk_miles = TripTimeWlk_hours * 3.0   ; walkspeed = 3.0 miles/hour
        PRINT PRINTO=1 CSV=T LIST= '@token_access@','@token_path@','@token_egress@',
          Trips,
          TripDistLoc_miles,
          TripDistLRF_miles,
          TripDistExp_miles,
          TripDistHvy_miles,
          TripDistCom_miles,
          TripDistFer_miles,
          TripDistDrv_miles,
          TripDistWlk_miles
      endif
    ENDRUN
    endloop
  EndDistributeMultistep
endloop

Wait4Files files=CTRAMP1.script.end,
                 CTRAMP2.script.end,
                 CTRAMP3.script.end,
                 CTRAMP4.script.end,
                 CTRAMP5.script.end,
           printfiles = merge, deldistribfiles = t, CheckReturnCode = t

; combine the path files into one
* copy metrics\ITHIM\DistanceTraveledByFacilityType_transit_com.csv+metrics\ITHIM\DistanceTraveledByFacilityType_transit_hvy.csv+metrics\ITHIM\DistanceTraveledByFacilityType_transit_exp.csv+metrics\ITHIM\DistanceTraveledByFacilityType_transit_lrf.csv+metrics\ITHIM\DistanceTraveledByFacilityType_transit_loc.csv metrics\ITHIM\DistanceTraveledByFacilityType_transit.csv
; delete the individual ones
* del metrics\ITHIM\DistanceTraveledByFacilityType_transit_com.csv
* del metrics\ITHIM\DistanceTraveledByFacilityType_transit_hvy.csv
* del metrics\ITHIM\DistanceTraveledByFacilityType_transit_exp.csv
* del metrics\ITHIM\DistanceTraveledByFacilityType_transit_lrf.csv
* del metrics\ITHIM\DistanceTraveledByFacilityType_transit_loc.csv
