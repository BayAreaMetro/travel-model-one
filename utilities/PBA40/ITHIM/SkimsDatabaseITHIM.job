;
; Similar to model-files\scripts\database\SkimsDatabase.JOB but ITHIM cares about what portions of
; transit paths are on bus versus rail, so this gives further detail.
;
; ----------------------------------------------------------------------------------------------------------------

; walk speed should be set to the same value as in the mode choice UEC
token_walk_speed =  3.00

; max walk distance is just used here to avoid very large numbers printed in the data; it will not be used as a threshold in the analysis
token_max_walk_distance = 75.0

; bicycle speed should be set to the same value as in the mode choice UEC
token_bike_speed = 12.00

; max bicycle distance is just used here to avoid very large numbers printed in the data; it will not be used as a threshold in the analysis
token_max_bike_distance = 75.0

; only print the data for internal to internal flows
token_internal_zones = 1454

; dump time-period-specific databases
loop period = 1, 5
  
   ; set a debug origin
   token_debug_origin = '1026'
     
   ; set a debug destination
   token_debug_destination = '1'
     
   ; set a debug filename prefix
   token_debug_filename = 'logs\SkimsDatabase'
  
   ; a two letter token is used for each time period
   if (period = 1)   
      token_period = 'EA'   
  
   elseif (period = 2)   
      token_period = 'AM'    
  
   elseif (period = 3)   
      token_period = 'MD'    
  
   elseif (period = 4)   
      token_period = 'PM'
  
   elseif (period = 5)   
      token_period = 'EV'
      
   endif
   
   ; do each time of day as a separate process
   DistributeMultistep processid = 'ctramp', processNum = period, commpath = 'X:\commpath'
     
   ; total travel time by mode
   run pgm = matrix

      ; walk, generic transit, walk skim
      filei mati[3] = skims\trnskm@token_period@_wlk_trn_wlk.tpp
      
      ; drive, generic transit, walk skim
      filei mati[4] = skims\trnskm@token_period@_drv_trn_wlk.tpp
      
      ; walk, generic transit, drive skim
      filei mati[5] = skims\trnskm@token_period@_wlk_trn_drv.tpp

      ; distances are in the temp files
      filei mati[6] = skims\trnskm@token_period@_wlk_trn_wlk_temp.tpp
      filei mati[7] = skims\trnskm@token_period@_drv_trn_wlk_temp.tpp
      filei mati[8] = skims\trnskm@token_period@_wlk_trn_drv_temp.tpp

      ; block file containing highway parameters (see note above)
      read file = ctramp\scripts\block\hwyparam.block
      
      ; print header for time by mode for each of the three files 
      if (i==1) 
         list = "orig,dest,",
                "walk_wTrnW,ivtB_wTrnW,ivtR_wTrnW,wait_wTrnW,distB_wTrnW,distR_wTrnW,",
                "walk_wTrnD,ivtB_wTrnD,ivtR_wTrnD,wait_wTrnD,distB_wTrnD,distR_wTrnD,dtime_wTrnD,ddist_wTrnD,",
                "walk_dTrnW,ivtB_dTrnW,ivtR_dTrnW,wait_dTrnW,distB_dTrnW,distR_dTrnW,dtime_dTrnW,ddist_dTrnW", file = database\IthimSkimsDatabase@token_period@.csv
      endif

      if (i<=@token_internal_zones@)

         jloop

            if (j<=@token_internal_zones@)

              ; ITHIM time ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
               if (mi.3.IVT > 0)
                  walk_wTrnW = (mi.3.WACC + mi.3.WAUX + mi.3.WEGR)/100.0
                  ivtB_wTrnW = (mi.3.IVTLOC + mi.3.IVTEXP)/100.0
                  ivtR_wTrnW = (mi.3.IVTLRF + mi.3.IVTHVY + mi.3.IVTCOM)/100.0
                  wait_wTrnW = (mi.3.IWAIT + mi.3.XWAIT)/100.0

                  distB_wTrnW = (mi.6.distLOC + mi.6.distEXP)/100.0
                  distR_wTrnW = (mi.6.distLRF + mi.6.distHVY + mi.6.distCOM)/100.0
               else
                  walk_wTrnW = -999.0
                  ivtB_wTrnW = -999.0
                  ivtR_wTrnW = -999.0
                  wait_wTrnW = -999.0

                  distB_wTrnW = -999.0
                  distR_wTrnW = -999.0
               endif
               if (mi.4.IVT > 0 )
                  walk_dTrnW = (mi.4.WACC + mi.4.WAUX + mi.4.WEGR)/100.0
                  ivtB_dTrnW = (mi.4.IVTLOC + mi.4.IVTEXP)/100.0
                  ivtR_dTrnW = (mi.4.IVTLRF + mi.4.IVTHVY + mi.4.IVTCOM)/100.0
                  wait_dTrnW = (mi.4.IWAIT + mi.4.XWAIT)/100.0

                  distB_dTrnW = (mi.7.distLOC + mi.7.distEXP)/100.0
                  distR_dTrnW = (mi.7.distLRF + mi.7.distHVY + mi.7.distCOM)/100.0

                  dtime_dTrnW = (mi.4.DTIME)/100.0
                  ddist_dTrnW = (mi.4.DDIST)/100.0
               else
                  walk_dTrnW = -999.0
                  ivtB_dTrnW = -999.0
                  ivtR_dTrnW = -999.0
                  wait_dTrnW = -999.0
                  distB_dTrnW = -999.0
                  distR_dTrnW = -999.0
                  dtime_dTrnW = -999.0
                  ddist_dTrnW = -999.0
               endif
               if (mi.5.IVT > 0 )
                  walk_wTrnD = (mi.5.WACC + mi.5.WAUX + mi.5.WEGR)/100.0
                  ivtB_wTrnD = (mi.5.IVTLOC + mi.5.IVTEXP)/100.0
                  ivtR_wTrnD = (mi.5.IVTLRF + mi.5.IVTHVY + mi.5.IVTCOM)/100.0
                  wait_wTrnD = (mi.5.IWAIT + mi.5.XWAIT)/100.0

                  distB_wTrnD = (mi.8.distLOC + mi.8.distEXP)/100.0
                  distR_wTrnD = (mi.8.distLRF + mi.8.distHVY + mi.8.distCOM)/100.0

                  dtime_wTrnD = (mi.5.DTIME)/100.0
                  ddist_wTrnD = (mi.5.DDIST)/100.0
               else
                  walk_wTrnD = -999.0
                  ivtB_wTrnD = -999.0
                  ivtR_wTrnD = -999.0
                  wait_wTrnD = -999.0
                  distB_wTrnD = -999.0
                  distR_wTrnD = -999.0
                  dtime_wTrnD = -999.0
                  ddist_wTrnD = -999.0
               endif

              ; write the output ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
              list = i(8.0),",",j(8.0),
                     ",",walk_wTrnW(10.2),",",ivtB_wTrnW(10.2),",",ivtR_wTrnW(10.2),",",wait_wTrnW(10.2),",",distB_wTrnW(10.2),",",distR_wTrnW(10.2),
                     ",",walk_wTrnD(10.2),",",ivtB_wTrnD(10.2),",",ivtR_wTrnD(10.2),",",wait_wTrnD(10.2),",",distB_wTrnD(10.2),",",distR_wTrnD(10.2),",",dtime_wTrnD(10.2),",",ddist_wTrnD(10.2),
                     ",",walk_dTrnW(10.2),",",ivtB_dTrnW(10.2),",",ivtR_dTrnW(10.2),",",wait_dTrnW(10.2),",",distB_dTrnW(10.2),",",distR_dTrnW(10.2),",",dtime_dTrnW(10.2),",",ddist_dTrnW(10.2),
                     file = database\IthimSkimsDatabase@token_period@.csv

            endif ; internal j zones only

         endjloop

      endif ; internal i zones only

   endrun

   EndDistributeMultistep

endloop ; token_period
Wait4Files files = CTRAMP1.script.end, CTRAMP2.script.end, CTRAMP3.script.end, CTRAMP4.script.end, CTRAMP5.script.end,
                   printfiles = merge, deldistribfiles = t, CheckReturnCode = t

