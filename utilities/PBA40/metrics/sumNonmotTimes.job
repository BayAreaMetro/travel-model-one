; 4. Average Walk and Bike Distance
; 3. Walk and Bike Daily Trips by Income
; 5. Wlak and Bike Travel Time by Income
loop inc = 1,6
   if (inc = 1)  token_inc   = 'inc1'
   if (inc = 2)  token_inc   = 'inc2'
   if (inc = 3)  token_inc   = 'inc3'
   if (inc = 4)  token_inc   = 'inc4'
   if (inc = 5)  token_inc   = '_2074'
   if (inc = 6)  token_inc   = '_2064'
   
   DistributeMultistep processid = 'ctramp', processNum = inc, commpath = 'X:\commpath'   
   RUN PGM = MATRIX
  ; Read person trips for a transitmode for all time periods and Access+Egress combinations
  ; Sum them to get daily total transit trips by transitmode
     FILEI MATI[1] = "main\tripsEA@token_inc@.tpp"   
     FILEI MATI[2] = "main\tripsAM@token_inc@.tpp"   
     FILEI MATI[3] = "main\tripsMD@token_inc@.tpp"   
     FILEI MATI[4] = "main\tripsPM@token_inc@.tpp"   
     FILEI MATI[5] = "main\tripsEV@token_inc@.tpp" 
     
     FILEI MATI[6] = "skims\nonmotskm.tpp" 
     
     FILEO PRINTO[1] = "metrics\nonmot_times_@token_inc@.csv",APPEND=T 
     
     ; Daily Walk Trips
     MW[1] = mi.1.walk + mi.2.walk + mi.3.walk + mi.4.walk + mi.5.walk
     
     ; Daily Bike Trips
     MW[2] = mi.1.bike + mi.2.bike + mi.3.bike + mi.4.bike + mi.5.bike
     
     ; Walk Distance
     MW[3] = mi.6.distwalk
     
     ; Bike Distance
     MW[4] = mi.6.distbike
     
     ; Walk Speed (MPH)
     walk_spd = 3
     
     ; Bike Speed (MPH)
     bike_spd = 12
     
     ; Walk Time
     MW[5] = MW[3]/walk_spd
     
     ; Bike Time
     MW[6] = MW[4]/bike_spd
      
     MW[7]  = MW[1]*MW[3] ; Walk trip dist
     MW[8]  = MW[2]*MW[4] ; Bike trip dist
     MW[9]  = MW[1]*MW[5] ; Walk trip time
     MW[10] = MW[2]*MW[6] ; Bike trip time     
     
    JLOOP
      walk_tripdist= walk_tripdist + MW[7]
      bike_tripdist= bike_tripdist + MW[8]   
      walk_triptime= walk_triptime + MW[9]
      bike_triptime= bike_triptime + MW[10] 
      walk_trips = walk_trips + MW[1]
      bike_trips = bike_trips + MW[2] 
    ENDJLOOP
     AvgWalkDist = walk_tripdist/walk_trips
     AvgWalkTime = walk_triptime/walk_trips * 60
     AvgBikeDist = bike_tripdist/bike_trips
     AvgBikeTime = bike_triptime/bike_trips * 60

     if (('@token_inc@'='inc1')||('@token_inc@'='inc2')||('@token_inc@'='inc3')||('@token_inc@'='inc4'))
       Income = '@token_inc@'
       Age    = 'all'
     elseif ('@token_inc@'='_2074')
       Income = 'all'
       Age    = '20-74'
     elseif ('@token_inc@'='_2064')
       Income = 'all'
       Age    = '20-64'
     endif

          if(I = 1 & @inc@=1) PRINT PRINTO=1 CSV=T LIST= "Age","Income","Mode","Daily Trips","Total Time (Hours)","Total Dist","Avg Time (Min)", "Avg Dist"
          if(I = 1454) PRINT PRINTO=1 CSV=T LIST= Age,Income,"Walk",walk_trips,walk_triptime,walk_tripdist,AvgWalkTime,AvgWalkDist
          if(I = 1454) PRINT PRINTO=1 CSV=T LIST= Age,Income,"Bike",bike_trips,bike_triptime,bike_tripdist,AvgBikeTime,AvgBikeDist
   ENDRUN
   EndDistributeMultistep

endloop

Wait4Files files=CTRAMP1.script.end, CTRAMP2.script.end, CTRAMP3.script.end,
                 CTRAMP4.script.end, CTRAMP5.script.end, CTRAMP6.script.end,
           printfiles = merge, deldistribfiles = t, CheckReturnCode = t

; combine the income files into one
* copy metrics\nonmot_times_inc1.csv+metrics\nonmot_times_inc2.csv+metrics\nonmot_times_inc3.csv+metrics\nonmot_times_inc4.csv+metrics\nonmot_times__2074.csv+metrics\nonmot_times__2064.csv metrics\nonmot_times.csv
; delete the individual ones
* del metrics\nonmot_times_inc1.csv
* del metrics\nonmot_times_inc2.csv
* del metrics\nonmot_times_inc3.csv
* del metrics\nonmot_times_inc4.csv
* del metrics\nonmot_times__2074.csv
* del metrics\nonmot_times__2064.csv