; Simple script to tally SGR transit delay (person hours).

*del metrics\transit_delay.csv

; Loop thru transit modes
loop path = 1,5
  if (path = 1) token_path = 'com'  ; commuter rail or long-haul premium
  if (path = 2) token_path = 'hvy'  ; heavy rail or medium-haul premium
  if (path = 3) token_path = 'exp'  ; express bus or medium-haul basic
  if (path = 4) token_path = 'lrf'  ; light rail (or ferry) or short-haul premium
  if (path = 5) token_path = 'loc'  ; local bus or short-haul basic

  DistributeMultistep processid = 'ctramp', processNum = path, commpath = 'X:\commpath' 
    loop accegg = 1,3
      if (accegg = 1)
         token_access = 'wlk'
         token_egress = 'wlk'
      elseif (accegg = 2)
         token_access = 'drv'
         token_egress = 'wlk'
      elseif (accegg = 3)
         token_access = 'wlk'
         token_egress = 'drv'
      endif

      RUN PGM = MATRIX
        FILEI MATI[1] = "main\tripsEA.tpp"
        FILEI MATI[2] = "main\tripsAM.tpp"
        FILEI MATI[3] = "main\tripsMD.tpp"
        FILEI MATI[4] = "main\tripsPM.tpp"
        FILEI MATI[5] = "main\tripsEV.tpp"

        ; these are in minutes*100
        FILEI MATI[6 ] = "skims\trnskmEA_@token_access@_@token_path@_@token_egress@_ivtt_delay.tpp"
        FILEI MATI[7 ] = "skims\trnskmAM_@token_access@_@token_path@_@token_egress@_ivtt_delay.tpp"
        FILEI MATI[8 ] = "skims\trnskmMD_@token_access@_@token_path@_@token_egress@_ivtt_delay.tpp"
        FILEI MATI[9 ] = "skims\trnskmPM_@token_access@_@token_path@_@token_egress@_ivtt_delay.tpp"
        FILEI MATI[10] = "skims\trnskmEV_@token_access@_@token_path@_@token_egress@_ivtt_delay.tpp"

        ; these are in minutes*100
        FILEI MATI[11] = "skims\trnskmEA_@token_access@_@token_path@_@token_egress@_board_delay.tpp"
        FILEI MATI[12] = "skims\trnskmAM_@token_access@_@token_path@_@token_egress@_board_delay.tpp"
        FILEI MATI[13] = "skims\trnskmMD_@token_access@_@token_path@_@token_egress@_board_delay.tpp"
        FILEI MATI[14] = "skims\trnskmPM_@token_access@_@token_path@_@token_egress@_board_delay.tpp"
        FILEI MATI[15] = "skims\trnskmEV_@token_access@_@token_path@_@token_egress@_board_delay.tpp"

        FILEO PRINTO[1] = "metrics\transit_delay_@token_path@.csv", append=T

        ; trips for this mode combination
        MW[1] = mi.1.@token_access@_@token_path@_@token_egress@
        MW[2] = mi.2.@token_access@_@token_path@_@token_egress@
        MW[3] = mi.3.@token_access@_@token_path@_@token_egress@
        MW[4] = mi.4.@token_access@_@token_path@_@token_egress@
        MW[5] = mi.5.@token_access@_@token_path@_@token_egress@

        ; delay in minutes*100
        MW[6 ] = mi.6.all_ivtt_delay  + mi.11.wait_board_delay
        MW[7 ] = mi.7.all_ivtt_delay  + mi.12.wait_board_delay
        MW[8 ] = mi.8.all_ivtt_delay  + mi.13.wait_board_delay
        MW[9 ] = mi.9.all_ivtt_delay  + mi.14.wait_board_delay
        MW[10] = mi.10.all_ivtt_delay + mi.15.wait_board_delay

        ; delay in person minutes*100
        MW[11] = MW[1]*MW[6 ]
        MW[12] = MW[2]*MW[7 ]
        MW[13] = MW[3]*MW[8 ]
        MW[14] = MW[4]*MW[9 ]
        MW[15] = MW[5]*MW[10]

        _transit_trips        = _transit_trips        + ROWSUM( 1) + ROWSUM( 2) + ROWSUM( 3) + ROWSUM( 4) + ROWSUM( 5)
        _delay_person_minutes = _delay_person_minutes + ROWSUM(11) + ROWSUM(12) + ROWSUM(13) + ROWSUM(14) + ROWSUM(15)

        IF (I=LASTZONE)
          IF (@path@ = 1 & @accegg@ = 1)
            PRINT PRINTO=1 CSV=T, LIST="Access","Mode","Egress","Person Hours Delay","Transit Trips"
          ENDIF

          ; person minutes * 100 => person hours
          _delay_person_hours = _delay_person_minutes/(60*100)
          PRINT PRINTO=1 CSV=T, LIST='@token_access@','@token_path@','@token_egress@',_delay_person_hours,_transit_trips
        ENDIF
      ENDRUN

    endloop
  EndDistributeMultistep
endloop


Wait4Files files=CTRAMP1.script.end, CTRAMP2.script.end, CTRAMP3.script.end, CTRAMP4.script.end, CTRAMP5.script.end,
           printfiles=merge, deldistribfiles=t, CheckReturnCode=t
; combine the path files into one
* copy metrics\transit_delay_com.csv+metrics\transit_delay_hvy.csv+metrics\transit_delay_exp.csv+metrics\transit_delay_lrf.csv+metrics\transit_delay_loc.csv metrics\transit_delay.csv
; delete originals
* del metrics\transit_delay_com.csv
* del metrics\transit_delay_hvy.csv
* del metrics\transit_delay_exp.csv
* del metrics\transit_delay_lrf.csv
* del metrics\transit_delay_loc.csv